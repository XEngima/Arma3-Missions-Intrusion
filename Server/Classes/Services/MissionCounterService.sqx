/*
 * Name:	MissionCounterService
 * Date:	2018-11-16
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Performs the countdown of the mission time.
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Server
{
	public class MissionCounterService
	{
		private static fields ["_mMatchLengthMinutes" as Scalar];
	
		// Gets whether the mission is running or not.
		public static property Boolean MissionIsRunning { get; private set; };
		
		public static property Scalar RemainingMinutes { get; private set; };

		// Performs the mission time count down, and informs all clients about remaining time
		// and when mission ends.
        private static method Run {
			private ["_endDayTime" as Scalar, "_lastReportMinute" as Scalar];
			
			_endDayTime = dayTime + (_mMatchLengthMinutes / 60);
			_lastReportMinute = _mMatchLengthMinutes;

			while { daytime < _endDayTime } do {
				_self.RemainingMinutes = floor ((_endDayTime - daytime) * 60) + 1;
				
				// If a report is not done for this minute
				if (_lastReportMinute != _self.RemainingMinutes) then
				{
					// If this minute is a reporting minute, report remaining time to players.
					if (_self.RemainingMinutes == 1 || 
						_self.RemainingMinutes == 2 || 
						_self.RemainingMinutes == 3 || 
						_self.RemainingMinutes == 4 || 
						_self.RemainingMinutes == 5 ||
						_self.RemainingMinutes == 10 ||
						_self.RemainingMinutes == 15 ||
						_self.RemainingMinutes == 20 ||
						_self.RemainingMinutes % 30 == 0) then
					{
						["ClientMessageHandler.ShowHint", ["Mission ends in " + str _self.RemainingMinutes + " minutes."]] call Remote.Invoke;
						_lastReportMinute = _self.RemainingMinutes;
					};
				};
				player sideChat str _remainingMinutes;
				sleep 5;
			};
			
			_self.MissionIsRunning = false;
			["MissionEndHandler.PlayEndScene", [ResourceLocationHandler.WinnersLocation.Side]] call Remote.Invoke;
        };
	
		// Starts the mission
		public static method RunAsync("_matchLengthMinutes" as Scalar) {
			_mMatchLengthMinutes = _matchLengthMinutes;
			_self.MissionIsRunning = true;
        	
			[] spawn _self.Run;
		};
	};
};
