/*
 * Name:	MotorizedDecisionMaker
 * Date:	2019-06-16
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Models the frontal lobe of the brain that is the centre for decision making.
 * This class is inherited by different squad brains with different perspective, like motorized, sharpshooter, air and naval.
 */

using Intrusion.Common;
using Intrusion.Server;

namespace Intrusion.Ai
{
	public class MotorizedDecisionMaker : VehicularDecisionMaker
	{
		public constructor("_time" as IMissionTime, "_knowledge" as Knowledge, "_actionServiceFactory" as IActionServiceFactory, "_world" as IWorld, "_actionPerformer" as IActionPerformer, "_markerHandler" as IMarkerHandler, "_actionTimeoutConfig" as IActionTimeoutConfig, "_resourceLocationConfig" as IResourceLocationConfig, "_randomGenerator" as IRandomGenerator)
		{
			[_time, _knowledge, _actionServiceFactory, _world, _actionPerformer, _markerHandler, _actionTimeoutConfig, _resourceLocationConfig, _randomGenerator] call _base.Constructor;
		};
		
		public override method OnNearEnemiesOnGroundSpotted("_enemy" as IUnitContainer, "_mountedVehicle" as VehicleContainer)
		{
			private ["_action" as MoveAction];
			
			// If acting as a profession squad
			
			if (!isNull _mountedVehicle && { !isNull _mountedVehicle.OwnerGroup }) then
			{
				_action = [(call _self.Time.GetMissionTime) + 60, MoveActionType.Engage, (call _enemy.GetPosition), "SAD", "LIMITED", "COMBAT", "RED", ""] new MoveAction;
				[AiSquadState.Moving, _action] call _self.SetState;
				[_action] call _self.ActionPerformer.DoMove;
	        	["Move action created (enemies spotted): " + str _action] call LogHandler.Debug;
			};
			
			[_enemy, _mountedVehicle] call _base.OnNearEnemiesOnGroundSpotted;
		};
		
		// Called when the group is closing in on a destination (not necessary a location).
		public override method OnClosingInOnDestination("_mountedVehicle" as VehicleContainer)
		{
			private ["_order" as Order, "_resourceLocation" as ResourceLocation, "_locationMarkerName" as String];
			
			private _time = call _self.Time.GetMissionTime;
			
			// If mounted on a squad vehicle.
			if (!isNull _mountedVehicle && { !isNull _mountedVehicle.OwnerGroup }) then
			{
				_order = _self.Knowledge.Order;
				_resourceLocation = _self.Knowledge.Order.ResourceLocation;
				_locationMarkerName = "";
				
				if (!isNull _resourceLocation) then {
					_locationMarkerName = _resourceLocation.MarkerName;
				};
				
				if (_order.Type == OrderType.Defend) then {
					private _action = [_time + _self.ActionTimeoutConfig.MoveActionTimeout, MoveActionType.Patrol, _self.Knowledge.Order.TargetPosition, "MOVE", "LIMITED", "COMBAT", "YELLOW", _locationMarkerName, 120] new MoveAction;
					[AiSquadState.Moving, _action] call _self.SetState;
					[_action as MoveAction] call _self.ActionPerformer.DoMove;
		        	["Move action created (patrol in): " + str _action] call LogHandler.Debug;
				}
				else {
					private _action = [_time + _self.ActionTimeoutConfig.MoveActionTimeout, MoveActionType.ClosingIn, _self.Knowledge.Order.TargetPosition, "MOVE", "LIMITED", "COMBAT", "YELLOW", _locationMarkerName, 30] new MoveAction;
					[AiSquadState.Moving, _action] call _self.SetState;
					[_action as MoveAction] call _self.ActionPerformer.DoMove;
		        	["Move action created (closing in): " + str _action] call LogHandler.Debug;
				};
				
	        	return;
			};
			
			[_mountedVehicle] call _base.OnClosingInOnDestination;
		};
		
		// Called when the squad is at a resource location and is performing a move action.
		// _mountedVehicle (VehicleContainer): The squad's mounted vehicle. classNull if no vehicle is mounted.
		// _locationMarkerName (String): The current location's marker name.
		// Returns (Boolean): true if the move action should be completed/interrupted. Otherwise false.
		protected override method Boolean CompleteMoveWhenAtResourceLocation("_mountedVehicle" as VehicleContainer, "_locationMarkerName" as String)
		{
			private ["_closestGroup" as Group];
		
			// If acting as a profession squad
			
			if (!isNull _mountedVehicle && { !isNull _mountedVehicle.OwnerGroup }) then
			{
				_closestGroup = [_locationMarkerName, side _self.Knowledge.Group] call _self.World.GetAlliedGroupClosestToLocation;
				return _closestGroup == _self.Knowledge.Group;
			};
			
			return [_mountedVehicle, _locationMarkerName] call _base.CompleteMoveWhenAtResourceLocation;
		};
	};
};
