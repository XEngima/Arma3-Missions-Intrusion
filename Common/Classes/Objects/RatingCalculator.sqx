/*
 * Name:	RatingCalculator
 * Date:	2019-04-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Calculates rating given to a group in different situations.
 */
namespace Intrusion.Common
{
	public class RatingCalculator
	{
		private fields ["_mRatingConfig" as IRatingConfig, "_mGameLengthMinutes" as Scalar];
		
		// Creates a RatingCalculator object.
		public constructor("_ratingConfig" as IRatingConfig, "_gameLengthMinutes" as Scalar)
		{
			_mRatingConfig = _ratingConfig;
			_mGameLengthMinutes = _gameLengthMinutes;
		};
		
		private method AddLargeGroupBonusAndGameLengthFactor("_rating" as Scalar, "_groupMemberCount" as Scalar)
		{
			return _rating * (_groupMemberCount + 1) * _mRatingConfig.GroupExtraMemberRatingFactor * (60 / _mGameLengthMinutes);
		};
		
		// Calculates how much rating each member in a group will get for getting a flag.
		// _groupMemberCount (Scalar): Number of units in the group.
		// Returns (Scalar): The rating to add to each unit in the group.
		public method Scalar CalculateGroupRatingFlag("_groupMemberCount" as Scalar)
		{
			return floor ([_mRatingConfig.CapturingFlag, _groupMemberCount] call _self.AddLargeGroupBonusAndGameLengthFactor);
		};
		
		// Calculates how much rating each member in a group will get for killing an enemy unit.
		// _groupMemberCount (Scalar): Number of units in the group.
		// Returns (Scalar): The rating to add to each unit in the group.
		public method Scalar CalculateGroupRatingEnemyKill("_groupMemberCount" as Scalar)
		{
			return floor ([_mRatingConfig.EnemyUnitKill, _groupMemberCount] call _self.AddLargeGroupBonusAndGameLengthFactor);
		};
		
		// Calculates how much rating each member in a group will get for killing an enemy unit.
		// _groupMemberCount (Scalar): Number of units in the group.
		// _supportVolume (Scalar): The support volume on a scala 0..1, (0.9 if supported vehicle had fuel 0.1 when support occurred.).
		// Returns (Scalar): The rating to add to each unit in the group.
		public method Scalar CalculateGroupRatingVehicleSupport("_groupMemberCount" as Scalar, "_supportVolume" as Scalar)
		{
			return floor _supportVolume * ([_mRatingConfig.VehicleSupport, _groupMemberCount] call _self.AddLargeGroupBonusAndGameLengthFactor);
		};
	};
};
