/*
 * Name:	AiSquadHandler
 * Date:	2019-04-12
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Handles all AI squads.
 */

using Intrusion.Common;

namespace Intrusion.Ai
{
	public class AiSquadHandler : IAiSquadHandler
	{
		private fields ["_mSquads" as Array /* of AiSquad */];
		
		// Creates a AiSquadHandler object.
		public constructor {
			_mSquads = [];
			
			_self.AiWarriorTypes = [
				ProfessionType.Motorized,
				ProfessionType.Mechanized,
				ProfessionType.Armored,
			];
		};
		
		public static property Array AiWarriorTypes { get; private set; };
		
		// Adds an AI squad to the AI squad handler.
		// _aiSquad (AiSquad): The AI squad to add.
		public method AddSquad("_aiSquad" as AiSquad)
		{
			_mSquads pushBack _aiSquad;
		};
		
		// Counts all AI units. Alive as well as dead units "waiting for respawn".
		// Returns (Scalar): The number of AI units.
		public method Scalar CountAllUnits()
		{
			private ["_count" as Scalar];
			
			_count = 0;
			
			{
				_count = _count + count _x.Members;
			} foreach _mSquads as AiSquad;
			
			_count
		};
		
		// Counts all AI units of a side. Alive as well as dead units "waiting for respawn".
		// _side (Side): The side to ask for.
		// Returns (Scalar): The number of AI units on the side.
		public method Scalar CountAllUnitsOfSide("_side" as Side)
		{
			private ["_count" as Scalar];
			
			_count = 0;
			
			{
				if (side _x.Group == _side) then {
					_count = _count + count _x.Members;
				};
			} foreach _mSquads as AiSquad;
			
			_count
		};
		
		// Gets all AI squads.
		// Returns (Array): All AI squads as an array of AiSquad.
		public method Array GetAllSquads()
		{
			_mSquads
		};
		
		// Gives a new order to an AI group.
		// _group (Group): The group to recieve the order.
		// _order (Order): The order.
		public method GiveOrderToGroup("_group" as Group, "_order" as Order)
		{
			scopeName "main";
			
			{
				if (_x.Group == _group) then {
					[_order] call _x.GiveOrder;
					breakOut "main";
				};
			} foreach _mSquads as AiSquad;
		};
	};
};
