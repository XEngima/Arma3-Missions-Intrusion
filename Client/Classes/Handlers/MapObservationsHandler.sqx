/*
 * Name:	MapObservationsHandler
 * Date:	2018-01-22
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Handles all map markers for the player on the current client.
 */

using Sqx.Markers;
using Intrusion.Common;
 
namespace Intrusion.Client
{
	public class MapObservationsHandler
	{
		private static fields ["_mMapObservationIds" as Array /* as Scalar */];
	
		// Initializes the MapObservationsHandler.
		public static method Init {
			_mMapObservationIds = [];
			[MapObservationsHandler.UpdateMap] call PublicVariableHandler.RegisterMapObservationsListener;
		};
		
		// Adds an observation marker to the map.
		// _mapObservation (MapObservation): The map observation to draw a marker for.
		private static method AddMarker {
			params ["_mapObservation" as MapObservation];
			private ["_name" as String, "_color" as String];
			
			_name = "INT_MapObservationMarker_" + str _mapObservation.Id;
			
			createMarkerLocal [_name, _mapObservation.PercievedPos];
			_name setMarkerTypeLocal "hd_dot";
			
			_color = "ColorGrey";
			if (_mapObservation.IsIdentifiedAsEnemy) then {
				_color = "ColorRed";
			};
			
			_name setMarkerColorLocal _color;
		};
		
		// Updates an existing observation marker on the map.
		// _mapObservation (MapObservation): The map observation marker to update its marker for.
		private static method UpdateMarker {
			params ["_mapObservation" as MapObservation];
			private ["_name" as String, "_color" as String];
			
			_name = "INT_MapObservationMarker_" + str _mapObservation.Id;
			
			_name setMarkerPosLocal _mapObservation.PercievedPos;
			_name setMarkerTypeLocal "hd_dot";
			
			_color = "ColorGrey";
			if (_mapObservation.IsIdentifiedAsEnemy) then {
				_color = "ColorRed";
			};
			
			_name setMarkerColorLocal _color;
		};
		
		// Deletes an observation marker on the map.
		// _mapObservationId (Scalar): The ID for the map observation marker to delete.
		private static method DeleteMarker {
			params ["_mapObservationId" as Scalar];
			private ["_name" as String];
			
			_name = "INT_MapObservationMarker_" + str _mapObservationId;
			
			deleteMarker _name;
		};
		
		// Updates all map observations on the map.
		public static method UpdateMap {
			params ["_mapObservations" as Array];
			private ["_newMapObservationIds" as Array];
			[(str _this) + " MapObservationsHandler.UpdateMap"] call LogHandler.Debug;
			
			_newMapObservationIds = [];
			
			// Add markers that do not already exist on the map
			{
				if (!(_x.Id in _mMapObservationIds)) then {
					[_x] call MapObservationsHandler.AddMarker;
					_mMapObservationIds pushBack _x.Id;
				}
				else { // Update the others
					[_x] call MapObservationsHandler.UpdateMarker;
				};
				
				_newMapObservationIds pushBack _x.Id;
			} foreach _mapObservations as MapObservation;
			
			// Remove markers that no lonoger exist as observations
			{
				if (!(_x in _newMapObservationIds)) then {
					[_x] call MapObservationsHandler.DeleteMarker;
				};
			} foreach _mMapObservationIds as Scalar;
			
			_mMapObservationIds = _newMapObservationIds;
		};
	};
};
