/*
 * Name:	MissionParametersHandler
 * Date:	2019-03-11
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Handlers all about admin selectable mission parameters.
 */
namespace Intrusion.Common
{
	public class MissionParametersHandler
	{
		// Creates a MissionParametersHandler object.
		public constructor {
			call _self.SetDefaultValuesIfSinglePlayer;
			
			_self.MatchLengthMinutes = paramsArray select 0;
			_self.UseTeamKillPrison = if (paramsArray select 1 == 1) then { true } else { false };
			_self.GuerillaGuardingResourceLocations = if (paramsArray select 2 == 1) then { true } else { false };
			_self.AiWestFrequency = (paramsArray select 3) as AiFrequency;
			_self.AiEastFrequency = (paramsArray select 4) as AiFrequency;
			_self.AiSkill = (paramsArray select 5) as Skill;
		};
		
		public property Scalar MatchLengthMinutes { get; private set; };
		
		public property Boolean UseTeamKillPrison { get; private set; };
		
		public property Boolean GuerillaGuardingResourceLocations { get; private set; };
		
		public property AiFrequency AiWestFrequency { get; private set; };
		
		public property AiFrequency AiEastFrequency { get; private set; };
		
		public property Skill AiSkill { get; private set; };
		
		public method SetDefaultValuesIfSinglePlayer()
		{
		    if (isNil "paramsArray") then
		    {
		        private ["_c" as Scalar, "_i" as Scalar, "_paramName" as String];
		        
		        paramsArray = [];
		        
		        if (isClass (missionConfigFile/"Params")) then
		        {
		            _c = count (missionConfigFile/"Params");
		            
		            for [{_i = 0}, {_i < _c}, {_i = _i + 1}] do
		            {
		                _paramName = configName ((missionConfigFile >> "Params") select _i);
		                paramsArray = paramsArray + [getNumber (missionConfigFile >> "Params" >> _paramName >> "default")];
		            };
		        };
		        
		        _c = count paramsArray;
		        
		        if (isNil "param1" && _c > 0) then {
		        	param1 = paramsArray select 0;
		        };
		        
		        if (isNil "param2" && _c > 1) then {
		        	param2 = paramsArray select 1;
		        };
		    };
		};
	};
};
