/*
 * Name:	TargetHandler
 * Date:	2019-02-09
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A helper class for some target logic.
 */

using Intrusion.Common;

namespace Intrusion.Server
{
	public class TargetHandler
	{
		private fields ["_mProfessionConfig" as ProfessionConfig, "_mInventedTechnologiesCollection" as InventedTechnologiesCollection];
	
		public constructor("_professionConfig" as ProfessionConfig, "_inventedTechnologiesCollection" as InventedTechnologiesCollection)
		{
			_mProfessionConfig = _professionConfig;
			_mInventedTechnologiesCollection = _inventedTechnologiesCollection;
		};
		
		// Gets a unit's all enemy targets
		// _unit (Object): The unit for which to check enemy targets.
		// _maxRange (Scalar): Optional. The maximun range from the unit to check.
		public method Array GetEnemyTargets("_unit" as Object, ["_maxRange" as Scalar, 1000])
		{
			private ["_targets" as Array, "_resultingTargets" as Array, "_visibility" as Scalar];
			private ["_squadCollection" as SquadCollection, "_squad" as Squad, "_professionType" as ProfessionType, "_intrusionVehicles" as Array /* of IntrusionVehicle */];
			private ["_targetUnitNearItsIntrusionVehicle" as Boolean, "_targetUnitHasIntrusionVehicle" as Boolean];
			_resultingTargets = [];
			
			// Reveal all units to the unit
			{
				if (alive _x && !([side group _unit, side _x] call BIS_fnc_sideIsFriendly)) then {
					if ([objNull, "VIEW"] checkVisibility [eyePos vehicle _unit, eyePos vehicle _x] > 0.1) then {
						_unit reveal _x;
						_unit reveal vehicle _x;
					};
				};
			} foreach allUnits;
			
    		// Get all targets within range
	        _targets = _unit nearTargets _maxRange;
	        _squadCollection = call PublicVariableHandler.GetSquadCollection;
			_intrusionVehicles = call PublicVariableHandler.GetIntrusionVehicles;
	        
			{
			    private ["_percievedPos" as Array, "_percievedSide" as Side, "_targetUnit" as Object, "_accuracy" as Scalar, "_profession" as Profession, "_technologyInvented" as Boolean];
			    
			    _percievedPos = _x select 0;
			    _percievedSide = _x select 2;
			    _targetUnit = _x select 4;
			    _accuracy = _x select 5;
			    
				if (_targetUnit isKindOf "Man" && alive _targetUnit && { !([side group _unit, _percievedSide] call BIS_fnc_sideIsFriendly) && _accuracy < 25 }) then
				{
					_professionType = ProfessionType.Undefined;
					
					if (_unit knowsAbout _targetUnit > 1) then {
						if (_unit knowsAbout _targetUnit > 3) then
						{
							_squad = [group _targetUnit] call _squadCollection.GetSquadByGroup;
							
							// If the profession type is a warrior type, the observing unit is guessing depending on nearby vehicles
							if (!isNull _squad) then
							{
								_profession = [_squad.ProfessionType] call _mProfessionConfig.GetProfession;
								
								_technologyInvented = true;								
								if (!isNull _profession) then {
									_technologyInvented = [side group _unit, _profession.RequiredTechnology] call _mInventedTechnologiesCollection.SideTechnologyIsInvented;
								};
								
								if (_technologyInvented) then
								{
									// Check if the unit is near its profession vehicle
									_targetUnitNearItsIntrusionVehicle = false;
									_targetUnitHasIntrusionVehicle = false;
									
									{
										if (_x.OwnerGroup == _squad.Group) then {
											_targetUnitHasIntrusionVehicle = true;
											
											if (vehicle _targetUnit distance _x.Vehicle < 30 && alive _x.Vehicle) then {
												_targetUnitNearItsIntrusionVehicle = true;
											};
										};
									} foreach _intrusionVehicles as IntrusionVehicle;
								
									if (!_targetUnitHasIntrusionVehicle || _targetUnitNearItsIntrusionVehicle) then {
										_professionType = _squad.ProfessionType;
									};
								}
								else {
									_professionType = ProfessionType.Infantry;
								};
							}
							else { // If the unit has not selected a profession (and is outside a vehicle), it should be infantry.
								_professionType = ProfessionType.Infantry;
							};
						};
					
						private _observedDistance = (vehicle _unit) distance (vehicle _targetUnit);
						_resultingTargets pushBack [side group _unit, _percievedPos, _percievedSide, _professionType, _targetUnit, _accuracy, _observedDistance] new Target;
					};
				};
			} foreach _targets as Array;
			
			// Add units in vehicles manually
			{
				private ["_targetUnit" as Object, "_vehicleHeight" as Scalar, "_squadsIntrusionVehicles" as Array, "_targetUnitInItsIntrusionVehicle" as Boolean, "_profession" as Profession, "_percievedSide" as Side];
				_targetUnit = _x as Object;
				
				if (vehicle _targetUnit distance _unit < 1200) then {
					if (vehicle _targetUnit != _targetUnit) then { // If unit is in a vehicle
						_vehicleHeight = ((boundingBox vehicle _targetUnit) select 1) select 2;
						_visibility = [objNull, "VIEW"] checkVisibility [eyePos _unit, [getPosASL vehicle _targetUnit select 0, getPosASL vehicle _targetUnit select 1, (getPosASL vehicle _targetUnit select 2) + _vehicleHeight + 0.5]];
						
						if (_visibility > 0.1 && { !([side group _unit, side _targetUnit] call BIS_fnc_sideIsFriendly) }) then
						{
							_professionType = ProfessionType.Undefined;
							_percievedSide = sideUnknown;
							
							// If they are in their profession vehicle, the profession is often known, if vehicle is close enough
							
							if (vehicle _targetUnit distance _unit < 700) then
							{
								_percievedSide = side _targetUnit;
								_squad = [group _targetUnit] call _squadCollection.GetSquadByGroup;
								if (!isNull _squad) then
								{
									_profession = [_squad.ProfessionType] call _mProfessionConfig.GetProfession;
									
									if (_profession.VehicleRevealsProfessionType) then
									{
										// Check if the unit is in its profession vehicle
										_targetUnitInItsIntrusionVehicle = false;
										{
											if (_x.OwnerGroup == _squad.Group) then {
												if (_x.Vehicle == vehicle _targetUnit) then {
													_targetUnitInItsIntrusionVehicle = true;
												};
											};
										} foreach _intrusionVehicles as IntrusionVehicle;
										
										if (_targetUnitInItsIntrusionVehicle) then {
											_professionType = _squad.ProfessionType;
										};
									};
								};
							
							};

							private _observedDistance = (vehicle _unit) distance (vehicle _targetUnit);
							_resultingTargets pushBack [side group _unit, getPos vehicle _targetUnit, _percievedSide, _professionType, _targetUnit, 0, _observedDistance] new Target;
						};
					};
				};
			} foreach allUnits as Object;
			
			_resultingTargets
		};
	};
};

