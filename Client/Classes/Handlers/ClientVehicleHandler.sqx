/*
 * Name:	ClientVehicleHandler
 * Date:	2018-12-09
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Handles some vehicle actions for the client.
 */
using Intrusion.Common;

namespace Intrusion.Client
{
	public class ClientVehicleHandler
	{
		private fields ["_mGroupMarkers" as Array /* of Object */];
		
		public constructor()
		{
			_mGroupMarkers = [];
		};
	
		// Adds the GetIn event handler to all vehicles in an array.
		// _vehicleArray (Array): Array of vehicles to add the event handler to.
		public method GroupLockVehicles("_vehicleArray" as Array /* of Object */)
		{
		    {
		        _x addEventHandler ["GetIn", {
		        	params ["_vehicle" as Object, "_bla" as Any, "_player" as Object];
				    private ["_intrusionVehicle" as IntrusionVehicle];
				    
                	_intrusionVehicle = _vehicle getVariable ["IntrusionVehicle", classNull];

					if (!isNull _intrusionVehicle) then
					{
						call gClient.ClientVehicleHandler.RemoveGroupMarkerFromVehicles;
					
						if (!(group _player isEqualTo _intrusionVehicle.OwnerGroup)) then {
							if (_intrusionVehicle.OwnerProfession == ProfessionType.Transport) then {
								if (_player == driver _intrusionVehicle.Vehicle) then {
									moveOut _player;
			                        ["You are not licenced for this vehicle.", _player] call ClientMessageHandler.ShowMessage;
								};
							}
							else {
								moveOut _player;
		                        ["You are not licenced for this vehicle.", _player] call ClientMessageHandler.ShowMessage;
							};
						};
					};
		        }];
		    } foreach _vehicleArray as Object;
		};
		
		// Adds an arrow above the vehicle to mark the vehicle for the owners.
		// _vehicleArray (Array): The vehicles to mark.
		public method AddGroupMarkerToVehicles("_vehicleArray" as Array /* of Object */)
		{
			private ["_groupMarker" as Object, "_vehicleHeight" as Scalar];
			
			call _self.RemoveGroupMarkerFromVehicles;
			
			{
				_vehicleHeight = ((boundingBox _x) select 1) select 2;
				_groupMarker = "Sign_Arrow_Large_Blue_F" createVehicle [getPosATL _x select 0, getPosATL _x select 1, (getPosATL _x select 2) + _vehicleHeight + 3];
				_groupMarker setPosATL [getPosATL _x select 0, getPosATL _x select 1, (getPosATL _x select 2) + _vehicleHeight + 3];
				_mGroupMarkers pushBack _groupMarker;
			} foreach _vehicleArray as Object;
		};
		
		// Removes the group marker arrow from above the vehicle.
		public method RemoveGroupMarkerFromVehicles()
		{
			{
				deleteVehicle _x;
			} foreach _mGroupMarkers as Object;
			
			_mGroupMarkers = [];
		};
	};
};
