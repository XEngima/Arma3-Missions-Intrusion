/*
 * Name:	RatingLock
 * Date:	2019-04-05
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Models a rating lock for a player.
 */
namespace Intrusion.Common
{
	public class RatingLock
	{
		private fields ["_mMissionTime" as IMissionTime];
	
		// Creates a RatingLock object.
		public constructor("_lockedUntilTime" as Scalar, "_reason" as RatingLockReason) {
			_mMissionTime = new MissionTime;
			_self.LockedUntilTime = _lockedUntilTime;
			_self.Reason = _reason;
			
			switch (_reason) do {
				case RatingLockReason.GroupIsSplit: { _self.ReasonText = "Group members too far apart"; };
				case RatingLockReason.LostVehicle: { _self.ReasonText = "Group lost vehicle"; };
				case RatingLockReason.Respawn: { _self.ReasonText = "Group member respawned"; };
				case RatingLockReason.TeamKill: { _self.ReasonText = "Group member team killed"; };
			};
		};
		
		// Gets the time when the player's rating lock is released.
		public property Scalar LockedUntilTime { get; private set; };
		
		// Gets the reason for the rating lock.
		public property RatingLockReason Reason { get; private set; };
		
		public property String ReasonText { get; private set; };
		
		// Gets whether the lock is active or not.
		public method Boolean IsActive()
		{
			(call _mMissionTime.GetMissionTime) < _self.LockedUntilTime
		};
	};
};
