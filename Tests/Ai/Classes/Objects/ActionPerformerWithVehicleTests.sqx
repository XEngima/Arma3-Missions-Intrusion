/*
 * Name:	ActionPerformerWithVehicleTests
 * Date:	2019-05-17
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Test class for AiBrain, that tests basic squad behaviour for infantry squads.
 */

using Sqx.UnitTest;
using Intrusion.Tests.Helpers;
using Intrusion.Ai;
using Intrusion.Common;
using Intrusion.Server;

namespace Intrusion.Tests.Ai
{
	public class ActionPerformerWithVehicleTests
	{
		#region Fields
		
		private static fields [
			"_mGroup" as Group,
			"_mWorld" as TestWorld,
			"_mMarkerHandler" as TestMarkerHandler,
			"_mActionPerformer" as TestActionPerformer,
			"_mBrain" as AiSquadBrain,
			"_mKnowledge" as Knowledge,
			"_mRandomGenerator" as TestRandomGenerator,
			"_mLocationFactory1" as ResourceLocation,
			"_mLocationOffice1" as ResourceLocation,
			"_mLocationFactory2" as ResourceLocation,
			"_mLocationOffice2" as ResourceLocation,
			"_mResourceLocationCollection" as ResourceLocationCollection,
			"_mTime" as TestMissionTime,
			"_mActionService" as TestActionService,
			"_mActionServiceFactory" as TestActionServiceFactory,
			"_mInventedTechnologiesCollection" as InventedTechnologiesCollection,
			"_mSquadHandler" as TestSquadHandler
		];
		
		#endregion

		// Registers all tests that should be run.
		public static method Init {
			["Intrusion.Tests.Ai", "ActionPerformerWithVehicleTests", [
				"MovingFarHasMountedVehicle_Act_StatusMoving",
				"MountedArrivingFarMoving_Act_StatusIdle",
				"MountedCloseToDestinationIdle_Act_StatusDisembarking",
				"CloseToDestinationTimeoutWhenDisembarking_Act_StatusIdle",
				"MountedMovingDetectedEnemies_Act_StatusIdle",
				"MountedIdleDetectedEnemies_Act_StatusDisembarking",
			]] call TestEngine.Register;
		};
		
		private static method BeforeEach()
		{
			private ["_mapMarkerInfo" as MapMarkerInfo, "_decisionMaker" as DecisionMaker];

			_mResourceLocationCollection = new ResourceLocationCollection;
			_mActionService = new TestActionService;
			_mActionServiceFactory = [_mActionService] new TestActionServiceFactory;
			
			_mLocationFactory1 = ["FACTORY1", ResourceFacility.Factory, objNull] new ResourceLocation;
			_mLocationOffice1 = ["OFFICE1", ResourceFacility.TechLab, objNull] new ResourceLocation;
			_mLocationFactory2 = ["FACTORY2", ResourceFacility.Factory, objNull] new ResourceLocation;
			_mLocationOffice2 = ["OFFICE2", ResourceFacility.TechLab, objNull] new ResourceLocation;
			
			[_mLocationFactory1] call _mResourceLocationCollection.AddResourceLocation;
			[_mLocationOffice1] call _mResourceLocationCollection.AddResourceLocation;
			[_mLocationFactory2] call _mResourceLocationCollection.AddResourceLocation;
			[_mLocationOffice2] call _mResourceLocationCollection.AddResourceLocation;
			
			_mMarkerHandler = new TestMarkerHandler;
			["respawn_marker", [0, 0, 0]] call _mMarkerHandler.AddMarkerPos;
			["FACTORY1", [200, 200, 0]] call _mMarkerHandler.AddMarkerPos;
			["OFFICE1", [400, 400, 0]] call _mMarkerHandler.AddMarkerPos;
			["FACTORY2", [600, 600, 0]] call _mMarkerHandler.AddMarkerPos;
			["OFFICE2", [800, 800, 0]] call _mMarkerHandler.AddMarkerPos;
			
			_mGroup = createGroup west;
			_mWorld = [_mResourceLocationCollection] new TestWorld;
			_mActionPerformer = [_mWorld] new TestActionPerformer;
			_mRandomGenerator = [0] new TestRandomGenerator;
			_mKnowledge = [_mGroup, []] new Knowledge;
			_mTime = [0] new TestMissionTime;
			_mInventedTechnologiesCollection = new InventedTechnologiesCollection;
			private _testActionTimeoutConfig = new TestActionTimeoutConfig;
			private _professionConfig = new ProfessionConfig;
			_mSquadHandler = new TestSquadHandler;
			
			_mapMarkerInfo = [west, "respawn_marker", "carpark_marker", "weapondepot_marker", "flag_texture", "marker_icon_type"] new MapMarkerInfo;
			_decisionMaker = [_mTime, _mKnowledge, _mActionServiceFactory, _mWorld, _mActionPerformer, _mMarkerHandler, _testActionTimeoutConfig] new DecisionMaker;
			
			_mBrain = [
				_mGroup, 
				_mWorld, 
				_mActionPerformer, 
				_mRandomGenerator, 
				_mMarkerHandler, 
				_testActionTimeoutConfig,
				_mTime, 
				_mapMarkerInfo,
				new TestAiWeaponsConfig,
				_mInventedTechnologiesCollection,
				_mKnowledge,
				_decisionMaker,
				new BaseConfig,
				_professionConfig,
				_mSquadHandler,
				new TestGameLengthMonitor
			] new AiSquadBrain;
		};
		
		private static method AfterEach()
		{
			deleteGroup _mGroup;
		};
		
		private static method MovingFarHasMountedVehicle_Act_StatusMoving
		{
			// Arrange
			_mWorld.LeaderPos = [10, 10, 0];
			_mWorld.ClosestEmptyFreeVehicle = [objNull, [15, 15, 0], 2] new VehicleContainer;
			_mWorld.SquadMounted = true;
			_mWorld.MountedVehicle = [objNull, [10, 10, 10], 4] new VehicleContainer;
			_mKnowledge.Members pushBack [[10, 10, 0]] new TestUnitContainer;
			_mKnowledge.Order = [_mGroup, OrderType.Attack, [600, 600, 0], _mLocationFactory2] new Order;
			_mKnowledge.State = AiSquadState.Idle;
			
			// Act
			call _mBrain.Act;
			
			// Assert
			private _expectedState = [AiSquadState.Moving] call AiSquadStateMeta.ToString;
			private _actualState = [_mKnowledge.State] call AiSquadStateMeta.ToString;
			[_expectedState, _actualState, "AI not moving with vehicle."] call Assert.AreEqual;
			[1, count _mActionPerformer.MoveActions, "AI did not perform a move action."] call Assert.AreEqual;
		};
		
		private static method MountedArrivingFarMoving_Act_StatusIdle
		{
			// Arrange
			_mWorld.LeaderPos = [550, 550, 0];
			_mWorld.SquadMounted = true;
			_mWorld.MountedVehicle = [objNull, [550, 550, 0], 4] new VehicleContainer;
			_mKnowledge.Members pushBack [[550, 550, 0]] new TestUnitContainer;
			_mKnowledge.Order = [_mGroup, OrderType.Attack, [600, 600, 0], _mLocationFactory2] new Order;
			_mKnowledge.State = AiSquadState.Moving;
			_mKnowledge.CurrentAction = [300, MoveActionType.ClosingIn, [600, 600, 0], "MOVE", "NORMAL", "SAFE", "YELLOW", "FACTORY2"] new MoveAction;
			
			// Act
			call _mBrain.Act;
			
			// Assert
			[AiSquadState.Idle, _mKnowledge.State, "AI does not stop moving."] call Assert.AreEqual;
			//[1, count _mActionPerformer.MoveActions, "AI did not perform a move action."] call Assert.AreEqual;
		};
		
		private static method MountedCloseToDestinationIdle_Act_StatusDisembarking
		{	
			private ["_action" as DisembarkAction];
			
			// Arrange
			_mWorld.LeaderPos = [550, 550, 0];
			_mWorld.SquadMounted = true;
			_mWorld.MountedVehicle = [objNull, [550, 550, 0], 4] new VehicleContainer;
			_mKnowledge.Members pushBack [[550, 550, 0]] new TestUnitContainer;
			_mKnowledge.Order = [_mGroup, OrderType.Attack, [600, 600, 0], _mLocationFactory2] new Order;
			_mKnowledge.State = AiSquadState.Idle;
			_mKnowledge.CurrentAction = [300, MoveActionType.Move, [600, 600, 0], "MOVE", "NORMAL", "SAFE", "YELLOW", "FACTORY2"] new MoveAction;
			
			// Act
			call _mBrain.Act;
			
			// Assert
			[AiSquadState.Disembarking, _mKnowledge.State, "AI not disembarking."] call Assert.AreEqual;
			_action = _mKnowledge.CurrentAction as DisembarkAction;
			[_action is DisembarkAction, "Action is not a disembark action."] call Assert.IsNotNull;
		};
		
		private static method CloseToDestinationTimeoutWhenDisembarking_Act_StatusIdle
		{	
			private ["_aiVehicle" as VehicleContainer];
			
			// Arrange
			_mWorld.LeaderPos = [550, 550, 0];
			_mWorld.SquadMounted = true;
			_mKnowledge.Members pushBack [[550, 550, 0]] new TestUnitContainer;
			_mKnowledge.Order = [_mGroup, OrderType.Attack, [600, 600, 0], _mLocationFactory2] new Order;
			_mKnowledge.State = AiSquadState.Disembarking;
			
			_aiVehicle = [objNull, [550, 550, 0], 2] new VehicleContainer;
			
			_mActionService.IsRunning = false;
			_mActionService.Success = false;
			
			_mKnowledge.CurrentAction = [300, _mActionService, _aiVehicle] new DisembarkAction;
			_mTime.MissionTime = 600;
			
			// Act
			call _mBrain.Act;
			
			// Assert
			[AiSquadState.Idle, _mKnowledge.State, "AI not disembarking."] call Assert.AreEqual;
		};
		
		private static method MountedMovingDetectedEnemies_Act_StatusIdle
		{
			// Arrange
			_mWorld.LeaderPos = [300, 300, 0];
			_mWorld.SquadMounted = true;
			_mWorld.MountedVehicle = [objNull, [300, 300, 0], 4] new VehicleContainer;
			_mWorld.ClosestDetectedEnemy = [[250, 300, 0]] new TestUnitContainer;
			_mKnowledge.Members pushBack [[300, 300, 0]] new TestUnitContainer;
			_mKnowledge.Order = [_mGroup, OrderType.Attack, [600, 600, 0], _mLocationFactory2] new Order;
			_mKnowledge.State = AiSquadState.Moving;
			_mKnowledge.CurrentAction = [300, MoveActionType.Move, [600, 600, 0], "MOVE", "NORMAL", "SAFE", "YELLOW", "FACTORY2"] new MoveAction;
			
			// Act
			call _mBrain.Act;
			
			// Assert
			[AiSquadState.Idle, _mKnowledge.State, "AI does not stop when detecting enemies."] call Assert.AreEqual;
		};
		
		private static method MountedIdleDetectedEnemies_Act_StatusDisembarking
		{
			private ["_action" as DisembarkAction];
		
			// Arrange
			_mWorld.LeaderPos = [300, 300, 0];
			_mWorld.SquadMounted = true;
			_mWorld.MountedVehicle = [objNull, [300, 300, 0], 4] new VehicleContainer;
			_mWorld.ClosestDetectedEnemy = [[250, 300, 0]] new TestUnitContainer;
			_mKnowledge.Members pushBack [[300, 300, 0]] new TestUnitContainer;
			_mKnowledge.Order = [_mGroup, OrderType.Attack, [600, 600, 0], _mLocationFactory2] new Order;
			_mKnowledge.State = AiSquadState.Idle;
			
			// Act
			call _mBrain.Act;
			
			// Assert
			private _expectedState = [AiSquadState.Disembarking] call AiSquadStateMeta.ToString;
			private _actualState = [_mKnowledge.State] call AiSquadStateMeta.ToString;
			[_expectedState, _actualState, "AI does not disembark when detecting enemies."] call Assert.AreEqual;
			
			_action = _mKnowledge.CurrentAction as DisembarkAction;
			[_action is DisembarkAction, "Action is not a disembark action."] call Assert.IsNotNull;
		};
	};
};
