/*
 * Name:    MissionEngine
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Executes the mission. Loops through the mission loop that will run from mission start
 * to mission end.
 */

using Intrusion.Common;

namespace Intrusion.Server
{
	public class MissionEngine
	{
		#region Handlers
		
		// Gets the mission's squad handler.
		public static property SquadHandler SquadHandler { get; private set; };
		
		#endregion
	
        // Runs the mission.
        private static method Run {
        	private ["_sideManagers" as Array, "_vehicleSpawner" as VehicleSpawner];
			private ["_technologyConfig" as TechnologyConfig, "_messageHandler" as MessageHandler];
        	
        	// Create configs
        	_technologyConfig = new TechnologyConfig;
        	
        	// Create handlers
        	_messageHandler = new MessageHandler;
        	_self.SquadHandler = new SquadHandler;
        	
        	call ResourceLocationHandler.AquireAllResourceLocations;
        	
        	// Start the mission counter.
        	call MissionCounterService.RunAsync;
        	
        	// Start the weapon service
        	call WeaponsService.RunAsync;
        	
        	// Start the observation service
        	call ObservationService.RunAsync;
        	
        	// Create a side manager for each side.
        	_sideManagers = [];
    		_vehicleSpawner = new VehicleSpawner;
        	{
        		_sideManagers pushBack [_x, _vehicleSpawner, _self.SquadHandler, _technologyConfig, _messageHandler] new SideManager;
        	} foreach GameplayConfig.CompetingSides as Side;
        	
        	// Start the free vehicles service
        	[_vehicleSpawner] call FreeVehiclesService.RunAsync;
        	
			// Perform the mission digest loop
			while { MissionCounterService.MissionIsRunning } do
			{
				{
					private ["_sideManager" as SideManager];
					_sideManager = _x;
					
					call _sideManager.PerformTurn;
				} foreach _sideManagers as SideManager;
				
				sleep 10;
			};
		};
		
		// Starts the mission
		public static method RunAsync {
			[] spawn _self.Run;
		};
	};
};
