/*
 * Name:    IntrusionMenuHandler
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Encapsulates all logic for the Intrusion Menu.
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Client
{
	public class IntrusionMenuHandler
	{
        // Initializes the Intrusion Menu for the player and adds the "Open Intrusion Menu" action.
        public static method Init {
        	_self.OptionLeaveGroupVisible = false;
        	_self.OptionClaimBrigadeLeadershipVisible = !didJIP;
        
			call _self.BuildIntrusionMenu;
			
			player addAction ["Open Intrusion Menu", { call IntrusionMenuHandler.Open; }];
		};
		
		public static property Boolean OptionLeaveGroupVisible { get; private set; };
		
		public static method ShowOptionLeaveGroup("_show" as Boolean)
		{
			if (_show != _self.OptionLeaveGroupVisible) then {
				_self.OptionLeaveGroupVisible = _show;
				call _self.BuildIntrusionMenu;
			};
		};
		
		public static property Boolean OptionClaimBrigadeLeadershipVisible { get; private set; };
		
		public static method ShowOptionClaimBrigadeLeadership("_show" as Boolean)
		{
			if (_show != _self.OptionClaimBrigadeLeadershipVisible) then {
				_self.OptionClaimBrigadeLeadershipVisible = _show;
				call _self.BuildIntrusionMenu;
			};
		};
		
		private static method BuildIntrusionMenu()
		{
//			INT_IntrusionMenu = 
//			[
//			  ["Intrusion Menu", true],
//			  ["Leave group", [0], "", -5, [["expression", "call Intrusion_Client_IntrusionMenuHandler_OnLeaveGroup"]], "1", "1-IsLeader"],
//			];
			
			INT_IntrusionMenu = 
			[
			  ["Intrusion Menu", true]
			];
			
			if (_self.OptionLeaveGroupVisible) then {
				INT_IntrusionMenu pushBack ["Leave group", [0], "", -5, [["expression", "call Intrusion_Client_IntrusionMenuHandler_OnLeaveGroup"]], "1", "1-IsLeader"];
			};
			
			if (_self.OptionClaimBrigadeLeadershipVisible) then {
				INT_IntrusionMenu pushBack ["Claim brigade leadership", [0], "", -5, [["expression", "call Intrusion_Client_IntrusionMenuHandler_OnClaimBrigadeLeadership"]], "1", "1"];
			};
		};
		
		// Opens the Intrusion menu for the current player.
		public static method Open {
			showCommandingMenu "#USER:INT_IntrusionMenu";
		};
		
		// Called when the local player selects the "Leave Group" option.
		private static method OnLeaveGroup {
			[player] joinSilent grpNull;
			["GroupActionHandler.ResetInvitePlayerActions"] call Remote.Invoke;
		};
		
		// Called when the local player selects the "Leave Group" option.
		private static method OnClaimBrigadeLeadership {
			["ServerEventReciever.OnPlayerClaimBrigadeLeadership", [getPlayerUID player, playerSide]] call Remote.Invoke;
		};
	};
};
