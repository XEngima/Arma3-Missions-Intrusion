/*
 * Name:	World
 * Date:	2019-05-17
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A class for the brain to have its decitions manifested in physical actions outside itself.
 */

using Intrusion.Common;
using Intrusion.Server;

namespace Intrusion.Ai
{
	public class World : IWorld
	{
		private fields ["_mResourceLocationConfig" as IResourceLocationConfig, "_mVehicleSpawner" as IVehicleSpawner, "_mResourceLocationHandler" as IResourceLocationHandler];
		
		// Creates a World object.
		public constructor("_resourceLocationConfig" as IResourceLocationConfig, "_vehicleSpawner" as IVehicleSpawner, "_resourceLocationHandler" as IResourceLocationHandler)
		{
			_mResourceLocationConfig = _resourceLocationConfig;
			_mVehicleSpawner = _vehicleSpawner;
			_mResourceLocationHandler = _resourceLocationHandler;
		};
		
		// Gets a group's leader's position.
		// _group (Group): The group to get the leader's position from.
		// Returns (Array): The position of the leader.
		public method Array GetLeaderPos("_group" as Group)
		{
			getPos leader _group
		};
		
		// Gets the resource location collection.
		// Returns (ResourceLocationCollection): The resource location collection.
		public method ResourceLocationCollection GetResourceLocationCollection()
		{
			/*
			scopeName "main";
			
			if (call PublicVariableHandler.ResourceLocationCollectionHasValue) then {
				(call PublicVariableHandler.GetResourceLocationCollection) breakOut "main";
			};
			
			classNull
			*/
			
			call _mResourceLocationHandler.GetResourceLocationCollection
		};
		
		// Checks if a group is inside the perimiters of one or more resource locations.
		// _group (Group): The group to check.
		// _location (ResourceLocation): The resource location to check.
		// Returns (Boolean): true if any squad member is inside the perimiter of the resource location, otherwise false.
		public method Boolean CheckSquadPresenceAtResourceLocation("_group" as Group, "_location" as ResourceLocation)
		{
			scopeName "main";

			{
				if (_x distance (getMarkerPos _location.MarkerName) < _mResourceLocationConfig.AreaRadius) then {
					true breakOut "main";
				};
			} foreach units _group;
			
			false
		};

		// Gets the closest empty, unassigned, usable and free vehicle that fits the group.
		// _side (Side): Side of the vehicle.
		// _pos (Array): The relative position.
		// _squadSize (Scalar): The number of members in the squad.
		public method AiVehicle GetClosestEmptyFreeVehicle("_side" as Side, "_pos" as Array, "_squadSize" as Scalar)
		{
			private ["_intrusionVehicle" as IntrusionVehicle, "_seatsCount" as Scalar, "_vehicle" as Object];
			
			_intrusionVehicle = [_side, _pos, _squadSize] call _mVehicleSpawner.GetNearestEmptyFreeVehicle;
			_vehicle = _intrusionVehicle.Vehicle;
			
			_seatsCount = (count fullCrew [_vehicle, "driver", true]) + (count fullCrew [_vehicle, "commander", true] + (count fullCrew [_vehicle, "gunner", true]) + (count fullCrew [_vehicle, "turret", true]) + (count fullCrew [_vehicle, "cargo", true]));

			[_intrusionVehicle.Vehicle, getPos _intrusionVehicle.Vehicle, _seatsCount] new AiVehicle;
		};
		
		// Checks if all squad members are mounted in a vehicle.
		// _group (Group): The group to check.
		// Returns (Boolean): true if all members are mounted, otherwise false.
		public method Boolean CheckSquadMounted("_group")
		{
			scopeName "main";
			
			{
				if (vehicle _x == _x) then {
					false breakOut "main";
				};
			} foreach units _group as Object;
			
			true
		};
		
		// Gets the speed of a vehicle.
		// _vehicle (Object): The vehicle.
		public method Scalar GetSpeed("_vehicle" as Object)
		{
			speed _vehicle
		};
		
		// Gets a squad's current vehicle. objNull if no one of the members are not in a vehicle.
		public method AiVehicle GetMountedVehicle("_group" as Group)
		{
			private ["_vehicle" as Object, "_seatsCount" as Scalar];
		
			scopeName "main";
			
			{
				if (vehicle _x != _x) then {
					_vehicle = vehicle _x;
					_seatsCount = (count fullCrew [_vehicle, "driver", true]) + (count fullCrew [_vehicle, "commander", true] + (count fullCrew [_vehicle, "gunner", true]) + (count fullCrew [_vehicle, "turret", true]) + (count fullCrew [_vehicle, "cargo", true]));
					([vehicle _x, getPos vehicle _x, _seatsCount] new AiVehicle) breakOut "main";
				};
			} foreach units _group as Object;
			
			classNull
		};
	};
};
