/*
 * Name:    SideHandler
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Manages the resource collection for a side.
 */

using Intrusion.Common;

namespace Intrusion.Server
{
	public class SideManager
	{
		private fields [
			"_mResourceBook" as ResourceBook,
			"_mResourceCollector" as ResourceCollector,
			"_mProductionManager" as ProductionManager,
			"_mScientist" as Scientist];
		
        // Creates a SideHandler object.
        // Parameter(s):
        // _side (Side): The side to handle.
        public constructor(
			"_serverConfig" as IServerConfig, 
			"_commonConfig" as ICommonConfig, 
			"_side" as Side, 
			"_vehicleSpawner" as IVehicleSpawner, 
			"_squadHandler" as ISquadHandler, 
			"_messageHandler" as IMessageHandler, 
			"_inventedTechnologiesCollection" as InventedTechnologiesCollection)
		{
            _self.Side = _side;
            
            _mResourceBook = new ResourceBook;
            _mResourceCollector = [_serverConfig.Gameplay, _side, _mResourceBook] new ResourceCollector;
            _mScientist = [_side, _mResourceBook, _commonConfig.Technology, _messageHandler, _inventedTechnologiesCollection] new Scientist;
            _mProductionManager = [_serverConfig.ProfessionVehicles, _serverConfig.VehicleClassNames, _side, _vehicleSpawner, _mResourceBook, _squadHandler, _mScientist, _messageHandler, _commonConfig.Professions] new ProductionManager;
		};
		
		// Gets the side of the side handler.
		public property Side Side { get; private set; };
		
		// Performs a turn for the side. I.e. collects resources and creates vehicles etc.
		public method ResourceReport PerformTurn {
			private ["_factoryCount" as Scalar, "_techLabCount" as Scalar];
		
			call _mResourceCollector.CollectResources;
			call _mScientist.PerformResearch;
			call _mProductionManager.AddReservationsForSquadsWithNoVehicle;
			call _mProductionManager.PerformProduction;
			
			_factoryCount = [_self.Side] call ResourceLocationHandler.GetFactoriesCountBySide;
			_techLabCount = [_self.Side] call ResourceLocationHandler.GetTechLabsCountBySide;
			
			[_self.Side, _factoryCount, _techLabCount, _mResourceBook.TotalProduction, _mResourceBook.TotalResearch] new ResourceReport
		};
	};
};
