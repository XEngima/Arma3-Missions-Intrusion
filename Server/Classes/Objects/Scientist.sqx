/*
 * Name:	Scientist
 * Date:	2017-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A class modeling a side's R&D manager refining generated technoloty (i.e. unlocks new technology).
 */

using Sqx.Collections;
using Intrusion.Common;

namespace Intrusion.Server
{
	public class Scientist
	{
		private fields ["_mResourceBook" as ResourceBook, "_mTechnologyConfig" as ITechnologyConfig, "_mMessageHandler" as IMessageHandler];
		private fields ["_mInventedTechnologies" as Array /* of Technology (enum) */, "_mSide" as Side];
		private fields ["_mCurrentTechnology" as TechnologyInfo, "_mAllTechnologiesInvented" as Boolean];
	
		// Creates a Sceintist object.
		public constructor("_side" as Side, "_resourceBook" as ResourceBook, "_technologyConfig" as ITechnologyConfig, "_messageHandler" as IMessageHandler)
		{
			_mSide = _side;
			_mResourceBook = _resourceBook;
			_mTechnologyConfig = _technologyConfig;
			_mMessageHandler = _messageHandler;
			
			_mCurrentTechnology = [1 as Technology] call _mTechnologyConfig.GetTechnology;
			_mInventedTechnologies = [];
			_mAllTechnologiesInvented = false;
		};
		
		private method SetTechnologyAsInvented()
		{
			private ["_technology" as Technology];
			
			_mInventedTechnologies pushBack _mCurrentTechnology.Technology;
			["Your side has invented '" + _mCurrentTechnology.Name + "'", _mSide] call _mMessageHandler.ShowHint;
			
			// Get next technology to research
			_technology = (_mCurrentTechnology.Technology as Scalar) + 1 as Technology;

			if (_technology == Technology.EndOfTechnologies) then {
				_mAllTechnologiesInvented = true;
			}
			else {
				_mCurrentTechnology = [_technology] call _mTechnologyConfig.GetTechnology;
			};
		};
		
		// Performs research and development, and notifies players when different technology progress steps are achieved.
		public method PerformResearch()
		{
			if (!_mAllTechnologiesInvented) then {
				if (_mResourceBook.TotalResearch >= _mCurrentTechnology.RequiredResearch) then {
					call _self.SetTechnologyAsInvented;
				};
			};
		};

		// Gets whether a technology is invented yet or not.
		public method Boolean TechnologyIsInvented("_technology" as Technology)
		{
			_technology in _mInventedTechnologies
		};
	};
};
