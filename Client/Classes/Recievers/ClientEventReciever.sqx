/*
 * Name:	ClientEventReciever
 * Date:	2019-01-31
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Static class that handles all Intrusion events sent from the server.
 */

using Intrusion.Common;

namespace Intrusion.Client
{
	public class ClientEventReciever
	{
		// Called when a player was set as a new brigade leader.
		// _playerUid (String): The player's UID.
		// _side (Side): The player's side.
		public static method OnNewBrigadeLeader("_playerUid" as String, "_side" as Side, "_player" as Object)
		{
			private ["_orderCollection" as OrderCollection];
		
			if (_playerUid == getPlayerUID player) then {
				ClientState.IsBrigadeLeader = true;
				
				if (!call PublicVariableHandler.OrderCollectionHasValue) then {
					_orderCollection = new OrderCollection;
					[_orderCollection] call PublicVariableHandler.BroadcastOrderCollection;
				};
			};
			
			call IntrusionMenuHandler.UpdateVisibleOptions;
		};
		
		// Called when a player was set rejected as a new brigade leader.
		// _playerUid (String): The player's UID.
		// _side (Side): The player's side.
		public static method OnBrigadeLeaderRejected("_playerUid" as String, "_side" as Side)
		{
			if (_playerUid == getPlayerUID player) then {
				["Your claim for brigade leadership was rejected.", player] call ClientMessageHandler.ShowMessage;
			};
		};
		
		// Called when a player quits as brigade leader
		public static method OnBrigadeLeaderQuit("_playerUid" as String)
		{
			if (_playerUid == getPlayerUID player) then {
				ClientState.IsBrigadeLeader = false;
			};
			
			call IntrusionMenuHandler.UpdateVisibleOptions;
		};
		
		// Called when a player joined a group
		public static method OnPlayerJoinedGroup("_player" as Object, "_oldGroup" as Group, "_newGroup" as Group)
		{
			call GroupActionHandler.UpdateAllInvitePlayerActions;
			call IntrusionMenuHandler.UpdateVisibleOptions;
			
			if (_player == player) then {
				ClientState.MyGroup = _newGroup;
				ClientState.IsSquadLeader = false;
				call gClient.TaskHandler.UpdateWithGroupTask;
			};
		};
		
		// Called when a player left a group
		public static method OnPlayerLeftGroup("_player" as Object, "_oldGroup" as Group)
		{
			call GroupActionHandler.UpdateAllInvitePlayerActions;
			call IntrusionMenuHandler.UpdateVisibleOptions;
			
			if (_player == player) then {
				ClientState.MyGroup = group player;
				ClientState.IsSquadLeader = true;
				call gClient.TaskHandler.DeleteAllTasksAndReset;
			};
		};
		
		// Called when a JIP player has connected.
		public static method OnJipPlayerConnected("_uid" as String)
		{
			call GroupActionHandler.UpdateAllInvitePlayerActions;
		};
		
		// Called when a group leader is teleporting his/her group back to base.
		public static method OnGroupTeleportingToBase("_group" as Group)
		{
			if (_group == group player) then {
				call gClient.TeleportHandler.TeleportPlayerToBase;
			};
		};
		
		// Called when the brigade leader is giving an order to a group.
		// _order (Order): The order.
		public static method OnBrigadeLeaderGivingOrder("_order" as Order)
		{
			if (_order.Group == group player) then {
				[_order] call gClient.TaskHandler.CreateTaskFromOrder;
			};
		};
		
		// Called when a player was killed.
		// _unit (Object): The dead body.
		// _side (Side): The player's side.
		public static method OnPlayerKilled("_unit" as Object, "_killer" as Object, "_instigator" as Object, "_useEffects" as Boolean, "_side" as Side)
		{
			private ["_prisonService" as PrisonService];
			
			removeAllActions _unit;
			
			player sideChat str (_killer == player);
			player sideChat str ([_side, side _killer] call BIS_fnc_sideIsFriendly);
			
			if (_killer == player && [_side, side _killer] call BIS_fnc_sideIsFriendly) then {
				_prisonService = [1] new PrisonService;
				call _prisonService.RunAsync;
			};
		};
		
		// Called when a player disconnected from an MP game.
		public static method OnPlayerDisconnected("_unit" as Object, "_id" as Scalar, "_uid" as String, "_name" as String)
		{
			removeAllActions _unit;
		};
		
		// Called when the resource report collection is updated.
		public static method OnResourceReportCollectionUpdated("_resourceReportCollection" as ResourceReportCollection)
		{
			private ["_resourceReport" as ResourceReport];
			
			_resourceReport = [playerSide] call _resourceReportCollection.GetReport;
			[_resourceReport] call gClient.DisplayHandler.DisplayResourceReport;
		};
		
		public static method OnPlayerRespawn("_player" as Object, "_corpse" as Object)
		{
			if (_player == player) then {
				if (!isNull ClientState.MyGroup) then {
					[player] joinSilent ClientState.MyGroup;
					
					if (ClientState.IsSquadLeader) then {
						ClientState.MyGroup selectLeader player;
					};
				};
			};
		
			call GroupActionHandler.UpdateAllInvitePlayerActions;
		};
	};
};
