/*
 * Name:	ProfessionDialog
 * Date:	2017-11-29
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A dialog in which the user can change profession.
 */
using Intrusion.Common;

namespace Intrusion.Client
{
	public class ProfessionDialog
	{
		private static fields ["_mOptions" as Array /* of ProfessionType */];
	
		// Initializes the profession dialog.
		public static method Init
		{
			_mOptions = [];
			_self.IsOpen = false;
			_self.SelectedProfession = ProfessionType.Undefined;
//			call _self.AddAvailableProfessions;
		
			// Set the selected profession
//			lbSetCurSel [DialogControl.GroupProfessionCombo, 0];
		};
		
		// Gets or sets the profession type that the user has selected.
		public static property ProfessionType SelectedProfession { get; set; };
		
		// Gets whether the dialog is open or not.
		public static property Boolean IsOpen { get; private set; };
		
		// Reads the selected profession type from the combo box
		// Returns (Profession): The selected profession. classNull if no profession is selected.
		private static method ProfessionType GetSelectedProfessionTypeFromCombo {
			private _selectedIndex = lbCurSel (DialogControl.GroupProfessionCombo as Scalar);
			
			_mOptions select _selectedIndex
		};
		
		// Adds all currently available professions to the combo box.
		private static method AddAvailableProfessions {
			private ["_profession" as Profession, "_squads" as SquadCollection, "_options" as Array];
			
            // Add all available professions
            _options = [];
            
            if (call PublicVariableHandler.SquadsHasValue) then {
				_squads = call PublicVariableHandler.GetSquads;
		
				{
					_profession = _x;
					
					if ([_profession.Type] call _squads.ProfessionTypeAvailable) then {
						lbAdd [DialogControl.GroupProfessionCombo, _x.Name];
						_options pushBack _profession.Type;
					};
				} foreach (call ProfessionConfig.GetAllProfessions) as Profession;
            }
            else {
				{
					lbAdd [DialogControl.GroupProfessionCombo, _x.Name];
					_options pushBack _x.Type;
				} foreach (call ProfessionConfig.GetAllProfessions) as Profession;
            };
            
            _mOptions = _options;
		};
	
		// Shows the dialog.
		// Returns (DialogResult): A dialog result.
		public static method DialogResult ShowDialog {
			waitUntil { !dialog };
			
			// Create the dialog
            createDialog "IntProfessionDialog";
			
			call _self.AddAvailableProfessions;
			lbSetCurSel [DialogControl.GroupProfessionCombo, 0];
			
			_self.IsOpen = true;

			// Wait until dialog is closed
			waitUntil { !dialog };
			
			// Return OK.
			DialogResult.Ok
		};
		
		// Closes the dialog if it is open.
		public static method CloseDialog {
			if (_self.IsOpen) then {
				closeDialog 0;
			};
		};
		
		// Sends the group's selected profession to the server.
		// Called by the .hpp-dialog when the OK button is pressed.
		public static method OnOkButtonPressed {
			["ProfessionDialog.OnOkButtonPressed"] call LogHandler.Debug;
			
			ProfessionDialog.SelectedProfession = call _self.GetSelectedProfessionTypeFromCombo;
			
			["SquadHandler.SetSquadProfession", [group player, ProfessionDialog.SelectedProfession]] call Remote.Invoke;
		};		
		
		// Called by the .hpp-dialog when the Cancel button is pressed.
		public static method OnCancelButtonPressed {
			call _self.CloseDialog;
		};		
	};
};
