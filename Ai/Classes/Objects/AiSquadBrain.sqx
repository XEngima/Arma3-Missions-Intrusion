/*
 * Name:	AiSquadBrain
 * Date:	2019-05-17
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Models the brain of the squad leader that takes all decisions of the team.
 */

using Intrusion.Common;
using Intrusion.Server;
using Sqx.Collections;

namespace Intrusion.Ai
{
	public class AiSquadBrain
	{
		private fields ["_mKnowledge" as IKnowledge, "_mWorld" as IWorld, "_mActionPerformer" as IActionPerformer, "_mRandomGenerator" as IRandomGenerator,
						"_mMarkerHandler" as IMarkerHandler, "_mActionTimeoutConfig" as IActionTimeoutConfig, "_mTime" as IMissionTime];
	
		// Creates a AiSquadBrain object.
		public constructor
		(
			"_group" as Group,
			"_world" as IWorld, 
			"_actionPerformer" as IActionPerformer, 
			"_randomGenerator" as IRandomGenerator,
			"_mapMarkerInfo" as MapMarkerInfo,
			"_markerHandler" as IMarkerHandler,
			"_actionTimeoutConfig" as IActionTimeoutConfig,
			"_time" as IMissionTime,
			["_knowledge" as IKnowledge, classNull])
		{
			_self.Group = _group;
			_mWorld = _world;
			_mActionPerformer = _actionPerformer;
			_mRandomGenerator = _randomGenerator;
			//_mKnowledge.MapMarkerInfo = _mapMarkerInfo;
			_mMarkerHandler = _markerHandler;
			_mActionTimeoutConfig = _actionTimeoutConfig;
			_mTime = _time;
			
			if (!isNull _knowledge) then {
				_mKnowledge = _knowledge;
				
				_mKnowledge.Group = _group;
			}
			else {
				_mKnowledge = [_group] new Knowledge;
			};
		};
		
		public method Scalar GetMemberCount()
		{
			_mKnowledge.MembersCount
		};
		
		public property Group Group { get; private set; };
		
		// Creates a new order on own initiative.
		private method CreateOrderOnOwnInitiative()
		{
			private ["_resourceLocationCollection" as ResourceLocationCollection, "_location" as ResourceLocation, "_locations" as Array /* of ResourceLocation */, "_valuedLocations" as Array /* of ValuedObject */];
			private ["_count" as Scalar, "_index" as Scalar, "_distance" as Scalar];
			
			// Find the three enemy (or neutral) resource locations closest to the base and select a random one of them.

			_resourceLocationCollection = call _mWorld.GetResourceLocationCollection;
			
			if (!isNull _resourceLocationCollection) then
			{
				_locations = call _resourceLocationCollection.GetResourceLocations;
				_count = 0;
				_valuedLocations = [];
				
				{
					_location = _x;
					
					if (_location.Side != side _mKnowledge.Group) then {
						_distance = ([_location.MarkerName] call _mMarkerHandler.GetMarkerPos) distance ([_mKnowledge.Group] call _mWorld.GetLeaderPos);
						_valuedLocations pushBack [_location, _distance] new ValuedObject;
						_count = _count + 1;
					};
				} foreach _locations as ResourceLocation;
				
				_valuedLocations = [_valuedLocations] call Sorter.QSort;
				
				if (_count > 3) then {
					_count = 3;
				};
				
				if (_count > 0) then {
					_index = floor ([_count] call _mRandomGenerator.GetNumber);
					_location = ((_valuedLocations select _index) as ValuedObject).Object as ResourceLocation;
					_mKnowledge.Order = [_mKnowledge.Group, OrderType.Attack, [_location.MarkerName] call _mMarkerHandler.GetMarkerPos, _location] new Order;
				};
			};
		};
		
		private method MakeDecision()
		{
			private ["_locationCollection" as ResourceLocationCollection, "_locations" as Array /* of ResourceLocation */, "_pos" as Array];
			private ["_locationMarkerName" as String];
			
			scopeName "main";
			
			
			// If inside the perimiters of a resource location that should be captured, capture it!
			
			_locationCollection = call _mWorld.GetResourceLocationCollection;
			_locations = call _locationCollection.GetResourceLocations;
			
			{
				if ([_mKnowledge.Group, _x] call _mWorld.CheckSquadPresenceAtResourceLocation) then {
					[AiSquadState.Capturing] call _self.SetState;
					_pos = [_x.MarkerName] call _mMarkerHandler.GetMarkerPos;
					_mKnowledge.CurrentAction = [(call _mTime.GetMissionTime) + _mActionTimeoutConfig.CaptureActionTimeout, _pos, _x.MarkerName] new CaptureAction;
					[_mKnowledge.CurrentAction as CaptureAction] call _mActionPerformer.DoCapture;
					breakOut "main";
				};
			} foreach _locations as ResourceLocation;
			
			// If not already there, move towards order's target!
			
			if (([_mKnowledge.Group] call _mWorld.GetLeaderPos) distance _mKnowledge.Order.TargetPosition > 25) then
			{
				[AiSquadState.Moving] call _self.SetState;
				_locationMarkerName = "";
				
				if (!isNull _mKnowledge.Order.ResourceLocation) then {
					_locationMarkerName = _mKnowledge.Order.ResourceLocation.MarkerName;
				};
				
				_mKnowledge.CurrentAction = [(call _mTime.GetMissionTime) + _mActionTimeoutConfig.MoveActionTimeout, _mKnowledge.Order.TargetPosition, _locationMarkerName] new MoveAction;
				[_mKnowledge.CurrentAction as MoveAction] call _mActionPerformer.DoMove;
				breakOut "main";
			};
		};
		
		#region Completion Checks
		
		// Gets whether the group has completed a move action.
		private method Boolean HasCompletedMove()
		{
			private ["_moveAction" as MoveAction, "_hasCompleted" as Boolean, "_leaderPos" as Array];
			private ["_location" as ResourceLocation, "_locationCollection" as ResourceLocationCollection, "_currentAction" as IAction];
			
			_hasCompleted = false;
			
			//if ((_mKnowledge.CurrentAction) is MoveAction) then // TODO SQX: Generates error that 'required variable before is'.
			_currentAction = _mKnowledge.CurrentAction; // TODO SQX: This is the temporary workaround...
			
			if (_currentAction is MoveAction) then
			{
				// Move is completed if the squad is at the destination position
				_leaderPos = [_mKnowledge.Group] call _mWorld.GetLeaderPos;
				_moveAction = _currentAction as MoveAction;				
				_hasCompleted = _leaderPos distance _moveAction.Position < 20;
				
				// Move is completed if the order movement is related to a resource location that is already ours
				if (!_hasCompleted) then
				{
					_locationCollection = call _mWorld.GetResourceLocationCollection;
					_location = [_moveAction.LocationMarkerName] call _locationCollection.GetResourceLocationByMarkerName;
					
					if (_location.Side == side _mKnowledge.Group) then
					{
						_hasCompleted = true;
					};
				};
			};
			
			_hasCompleted
		};
		
		// Gets whether the group has completed a capture action, i.e. it is no longer present at a resource location with enemy side.
		private method Boolean HasCompletedCapture()
		{
			private ["_captureAction" as CaptureAction, "_hasCompleted" as Boolean];
			private ["_location" as ResourceLocation, "_locationCollection" as ResourceLocationCollection, "_currentAction" as IAction];
			
			_hasCompleted = false;
			_currentAction = _mKnowledge.CurrentAction;
			
			if (_currentAction is CaptureAction) then
			{
				_captureAction = _currentAction as CaptureAction;
				
				_locationCollection = call _mWorld.GetResourceLocationCollection;
				_location = [_captureAction.LocationMarkerName] call _locationCollection.GetResourceLocationByMarkerName;
				
				if (_location.Side == side _mKnowledge.Group) then {
					_hasCompleted = true;
				};
				
				
				if (!_hasCompleted) then {
					if (!([_mKnowledge.Group, _location] call _mWorld.CheckSquadPresenceAtResourceLocation)) then {
						_hasCompleted = true;
					};
				};
			};
			
			_hasCompleted
		};
		
		// Gets whether the last action has completed or not.
		// Returns (Boolean): true if the last action has completed, otherwise false.
		public method Boolean GetHasCompleted()
		{
			private ["_hasCompleted" as Boolean, "_currentAction" as IAction];
			
			_hasCompleted = false;
			_currentAction = _mKnowledge.CurrentAction;
			
			if (_currentAction is MoveAction) then {
				_hasCompleted = call _self.HasCompletedMove;
			};
			
			if (_currentAction is CaptureAction) then {
				_hasCompleted = call _self.HasCompletedCapture;
			};
			
			_hasCompleted
		};
		
		#endregion
		
		public method GiveOrder("_order" as Order)
		{
			_mKnowledge.Order = _order;
			_mKnowledge.OrderIsGivenByBrigadeLeader = true;
		};
		
		// Tracks the leader's movement, and stationary for so long time that squad status should be set to idle.
		// Returns (Boolean): true if an action timeout has occurred, otherwise false.
		private method Boolean CheckActionTimeout()
		{
			private ["_leaderPos" as Array, "_action" as IAction];
			
			scopeName "main";
			
			// If leader has moved since last time, update position and time
			
			_leaderPos = [_mKnowledge.Group] call _mWorld.GetLeaderPos;
			
			if (!(_leaderPos isEqualTo _mKnowledge.LeaderPos)) then {
				_mKnowledge.LeaderPos
			}
			else
			{
				// If leader has not moved since last time, check if current action should time out
				if ((call _mTime.GetMissionTime) > _mKnowledge.LeaderPosTime + _mActionTimeoutConfig.LeaderStationaryTimeout) then {
					true breakOut "main";
				};
			};
			
			// Check if the current action has timed out
			_action = _mKnowledge.CurrentAction;
			if (!isNull _action && { (call _mTime.GetMissionTime) >= _action.TimeoutTime }) then {
				true breakOut "main";
			};
			
			false
		};
		
		private method SetState("_state" as AiSquadState) 
		{
			_mKnowledge.State = _state;
			player sideChat "Setting status for " + str _mKnowledge.Group + " (" + str (count units _mKnowledge.Group) + "): " + ([_state] call AiSquadStateMeta.ToString);
		};
		
		// Gives the AI brain an opportunity to make a new decision.
		public method Act()
		{
			// If there is no order, create one on own initiative so the group has something to do.
			
			if (isNull _mKnowledge.Order) then {
				call _self.CreateOrderOnOwnInitiative;
			};
			
			// If the squad is not doing anything, make a new decision.
		
			if (_mKnowledge.State == AiSquadState.Idle) then {
				call _self.MakeDecision;
				
				// If still idle, create a new order
				if (_mKnowledge.State == AiSquadState.Idle) then {
					call _self.CreateOrderOnOwnInitiative;
				};
			}
			else
			{
				// If there has been a timeout or if the squad is complete doing something, set to idle.
				
				if (call _self.CheckActionTimeout || { call _self.GetHasCompleted }) then {
					[AiSquadState.Idle] call _self.SetState;
				};
			};
		};
	};
};
