/*
 * Name:	MissionCounterService
 * Date:	2018-11-16
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Performs the countdown of the mission time.
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Server
{
	public class MissionCounterService : Service, IGameLengthMonitor
	{
		public constructor("_matchLengthMinutes" as Scalar)
		{
			_self.MinutesFromStart = _matchLengthMinutes;
		};
		
		public property Scalar MinutesFromStart { get; private set; };
		
		public property Scalar RemainingMinutes { get; private set; };

		// Performs the mission time count down, and informs all clients about remaining time
		// and when mission ends.
        protected override method Run {
			private ["_endDayTime" as Scalar, "_lastReportMinute" as Scalar];
			
			_endDayTime = dayTime + (_self.MinutesFromStart / 60);
			_lastReportMinute = _self.MinutesFromStart;

			while { daytime < _endDayTime } do {
				_self.RemainingMinutes = floor ((_endDayTime - daytime) * 60) + 1;
				
				// If a report is not done for this minute
				if (_lastReportMinute != _self.RemainingMinutes) then
				{
					// If this minute is a reporting minute, report remaining time to players.
					if (_self.RemainingMinutes == 1 || 
						_self.RemainingMinutes == 2 || 
						_self.RemainingMinutes == 3 || 
						_self.RemainingMinutes == 4 || 
						_self.RemainingMinutes == 5 ||
						_self.RemainingMinutes == 10 ||
						_self.RemainingMinutes == 15 ||
						_self.RemainingMinutes == 20 ||
						_self.RemainingMinutes % 30 == 0) then
					{
						["ClientMessageHandler.ShowHint", ["Mission ends in " + str _self.RemainingMinutes + " minutes."]] call Remote.Invoke;
						_lastReportMinute = _self.RemainingMinutes;
					};
				};

				sleep 5;
			};
			
			["ClientEventReciever.OnMissionTimeIsUp"] call Remote.Invoke;
			["ServerEventReciever.OnMissionTimeIsUp"] call Remote.Invoke;
        };
	};
};
