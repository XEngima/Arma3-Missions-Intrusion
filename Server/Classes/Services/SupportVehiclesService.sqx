/*
 * Name:	SupportVehiclesService
 * Date:	2019-10-04
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A service that "heals" vehicles when a support vehicle comes near.
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Server
{
	public class SupportVehiclesService : Service
	{
		// Creates a SupportVehiclesService object.
		public constructor()
		{
		};
		
		private method Object GetClosestVehicleWithinHealDistance("_supportVehicle" as Object)
		{
			{
				if (_x != _supportVehicle) then {
					if (_x distance _supportVehicle < 15) then {
						return _x;
					};
				};
			} foreach vehicles;
			
			return objNull;
		};
		
		protected override method Run()
		{
			private ["_intrusionVehicle" as IntrusionVehicle, "_closeVehicle" as Object];
			
			while { true } do {
				{
					_intrusionVehicle = _x getVariable ["IntrusionVehicle", classNull];
					
					if (!isNull _intrusionVehicle && { _intrusionVehicle.OwnerProfession == ProfessionType.Repair }) then
					{
						_closeVehicle = [_intrusionVehicle.Vehicle] call _self.GetClosestVehicleWithinHealDistance;
						
						if (!isNull _closeVehicle && { alive _closeVehicle }) then
						{
							if (damage _closeVehicle > 0.01 || !canMove _closeVehicle) then {
								_closeVehicle setDamage 0;
								["ClientEventReciever.OnVehicleSupported", [_closeVehicle, _intrusionVehicle.Vehicle, "Your vehicle was repaired by allied repair vehicle.", "Vehicle was repaired."]] call Remote.Invoke;
							};
						};
					};
					
				} foreach vehicles;
				
				sleep 5;
			};
		
			call _base.Run;
		};
	};
};
