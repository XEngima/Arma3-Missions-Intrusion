/*
 * Name:	AiBrainTests
 * Date:	2019-05-17
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Test class for AiBrain.
 */

using Sqx.UnitTest;
using Intrusion.Tests.Helpers;
using Intrusion.Ai;
using Intrusion.Common;

namespace Intrusion.Tests.Ai
{
	public class AiSquadBrainTests
	{
		#region Fields
		
		private static fields [
			"_mGroup" as Group,
			"_mWorld" as TestWorld,
			"_mMarkerHandler" as TestMarkerHandler,
			"_mActionPerformer" as TestActionPerformer,
			"_mBrain" as AiSquadBrain,
			"_mKnowledge" as IKnowledge,
			"_mResourceLocationCollection" as ResourceLocationCollection,
			"_mRandomGenerator" as TestRandomGenerator,
		];
		
		#endregion

		// Registeres all tests that should be run.
		public static method Init {
			["Intrusion.Tests.Ai", "AiSquadBrainTests", [
				"StateIdleAndNoOrder_MakeDecision_NotIdleAndHasOrder",
				"AttackOrder100m_MakeDecision_StartingMove"
			]] call TestEngine.Register;
		};
		
		private static method BeforeEach()
		{
			private ["_resourceLocationCollection" as ResourceLocationCollection, "_mapMarkerInfo" as MapMarkerInfo];
			
			_resourceLocationCollection = new ResourceLocationCollection;
			[["FACTORY1", ResourceFacility.Factory, objNull] new ResourceLocation] call _resourceLocationCollection.AddResourceLocation;
			[["OFFICE1", ResourceFacility.TechLab, objNull] new ResourceLocation] call _resourceLocationCollection.AddResourceLocation;
			[["FACTORY2", ResourceFacility.Factory, objNull] new ResourceLocation] call _resourceLocationCollection.AddResourceLocation;
			[["OFFICE2", ResourceFacility.TechLab, objNull] new ResourceLocation] call _resourceLocationCollection.AddResourceLocation;
			
			["FACTORY1", [100, 100, 0]] call _mMarkerHandler.AddMarkerPos;
			["OFFICE1", [200, 200, 0]] call _mMarkerHandler.AddMarkerPos;
			["FACTORY2", [300, 300, 0]] call _mMarkerHandler.AddMarkerPos;
			["OFFICE2", [400, 400, 0]] call _mMarkerHandler.AddMarkerPos;
			
			_mResourceLocationCollection = _resourceLocationCollection;
			
			_mGroup = createGroup west;
			
			_mWorld = [_mResourceLocationCollection] new TestWorld;
			_mActionPerformer = new TestActionPerformer;
			_mKnowledge = [_mGroup] new Knowledge;
			
			_mapMarkerInfo = [west, "respawn_marker", "carpark_marker", "weapondepot_marker", "flag_texture", "marker_icon_type"] new MapMarkerInfo;
			
			_mBrain = [_mGroup, _mWorld, _mActionPerformer, _mRandomGenerator, _mapMarkerInfo, _mKnowledge] new AiSquadBrain;
		};
		
		private static method AfterEach()
		{
			deleteGroup _mGroup;
		};
		
		private static method StateIdleAndNoOrder_MakeDecision_NotIdleAndHasOrder
		{
			// Arrange
			
			// Act
			call _mBrain.Act;
			
			// Assert
			[AiSquadState.Idle, _mKnowledge.State, "AI did not make a decision."] call Assert.AreNotEqual;
			[_mKnowledge.Order, "AI did not create an order."] call Assert.IsNotNull;
		};
		
		private static method AttackOrder100m_MakeDecision_StartingMove
		{
			private ["_moveAction" as MoveAction];
		
			// Arrange
			_mWorld.LeaderPos = [0, 0, 0];
			_mKnowledge.Order = [_mGroup, OrderType.Attack, [0, 100, 0]] new Order;
			_mRandomGenerator.Number = 0;
			
			// Act
			call _mBrain.Act;
			
			// Assert
			[1, count _mActionPerformer.MoveActions, "AI did not decide to perform a move action."] call Assert.AreEqual;
			
			_moveAction = _mActionPerformer.MoveActions select 0;
			[[100, 100, 0], _moveAction.TargetPos, "AI moves to wrong place."] call Assert.AreEqual;
		};	
	};
};
