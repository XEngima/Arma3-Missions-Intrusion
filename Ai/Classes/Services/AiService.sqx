/*
 * Name:	AiService
 * Date:	2019-02-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * The service that performs actions on all AI squads.
 */

using Intrusion.Common;
using Intrusion.Server;

namespace Intrusion.Ai
{
	public class AiService : IService
	{
		private fields ["_mUnitBalancer" as IAiUnitBalancer, "_mGameplayConfig" as IGameplayConfig, "_mAiSquadHandler" as IAiSquadHandler /* of AiSquad */];
		private fields ["_mMapMarkersConfig" as IMapMarkersConfig, "_mSquadHandler" as ISquadHandler];
		private fields ["_mResourceLocationHandler" as IResourceLocationHandler, "_mUnitsConfig" as IAiUnitsConfig];
		private fields ["_mUnitSpawner" as IUnitSpawner, "_mResourceLocationConfig" as IResourceLocationConfig, "_mVehicleSpawner" as IVehicleSpawner];
		
		// Creates a AiService object.
		public constructor(
			"_aiSquadHandler" as IAiSquadHandler, 
			"_unitBalancer" as IAiUnitBalancer, 
			"_gameplayConfig" as IGameplayConfig, 
			"_mapMarkersConfig" as IMapMarkersConfig, 
			"_professinConfig" as IProfessionConfig, 
			"_squadHandler" as ISquadHandler, 
			"_resourceLocationHandler" as IResourceLocationHandler, 
			"_unitSpawner" as IUnitSpawner,
			"_resourceLocationConfig" as IResourceLocationConfig,
			"_unitsConfig" as IAiUnitsConfig,
			"_vehicleSpawner" as IVehicleSpawner)
		{
			_mAiSquadHandler = _aiSquadHandler;
			_mUnitBalancer = _unitBalancer;
			_mGameplayConfig = _gameplayConfig;
			_mMapMarkersConfig = _mapMarkersConfig;
			_mSquadHandler = _squadHandler;
			_mResourceLocationHandler = _resourceLocationHandler;
			_mUnitSpawner = _unitSpawner;
			_mResourceLocationConfig = _resourceLocationConfig;
			_mUnitsConfig = _unitsConfig;
			_mVehicleSpawner = _vehicleSpawner;
		};
		
		public property Boolean IsRunning { get; private set; };
		
//		private method SetProfessionTypeDelayed("_side" as Side, "_unitCount" as Scalar, "_aiSquad" as AiSquad)
//		{
//			private ["_professionType" as ProfessionType];
//			
//			sleep random 120;
//			
//			_professionType = [_side, _unitCount] call _self.GetRandomProfessionType;
//			[_aiSquad.Group, _professionType] call _mSquadHandler.SetSquadProfession;
//		};
		
		// Creates a new squad if more squads are alive.
		private method CreateNewSquads()
		{
			private ["_group" as Group, "_mapMarkerInfo" as MapMarkerInfo, "_unitInfo" as AiUnitInfo];
			private ["_existingUnitsCount" as Scalar, "_maxUnitsCount" as Scalar, "_unitCount" as Scalar, "_aiSquad" as AiSquadBrain];

			{
				_existingUnitsCount = [_x] call _mAiSquadHandler.CountAllUnitsOfSide;
				_maxUnitsCount = [_x] call _mUnitBalancer.GetAllowedUnitCount;
				_unitCount = selectRandom [1, 2, 2, 2, 3];
				_mapMarkerInfo = [_x] call _mMapMarkersConfig.GetMapMarkerInfo;
				_unitInfo = [_x] call _mUnitsConfig.GetUnitInfo;

        		["Create new squads variables:"] call LogHandler.Debug;
        		["_existingUnitsCount: " + str _existingUnitsCount] call LogHandler.Debug;
        		["_maxUnitsCount: " + str _maxUnitsCount] call LogHandler.Debug;
        		["_unitCount: " + str _unitCount] call LogHandler.Debug;
        	
				if (_existingUnitsCount < _maxUnitsCount) then
				{
					_group = createGroup _x;
					
					if (_existingUnitsCount + _unitCount > _maxUnitsCount) then {
						_unitCount = _maxUnitsCount - _existingUnitsCount;
					};
					
					for "_i" from 1 to _unitCount do {
						[_unitInfo.UnitClassName, getMarkerPos _mapMarkerInfo.BaseMarkerName, _group] call _mUnitSpawner.CreateUnit;
					};
				
					_aiSquad = [
						_group,
						[_mResourceLocationConfig, _mVehicleSpawner] new World,
						[_group, _mResourceLocationHandler] new ActionPerformer, 
						new RandomGenerator, 
						new MarkerHandler, 
						new ActionTimeoutConfig, 
						new MissionTime,
						_mapMarkerInfo,
						new ActionServiceFactory] new AiSquadBrain;
					
					
					
					[_aiSquad] call _mAiSquadHandler.AddSquad;
					[_aiSquad.Group, ProfessionType.Infantry] call _mSquadHandler.SetSquadProfession;
					//[_x, _unitCount, _aiSquad] spawn _self.SetProfessionTypeDelayed;
				};
			} foreach _mGameplayConfig.CompetingSides as Side;
		};
		
		private method Run()
		{
			private ["_allSquads" as Array];
			
			while { true } do
			{
				_allSquads = call _mAiSquadHandler.GetAllSquads;
				call _self.CreateNewSquads;
				
				{
					call _x.Act;
					sleep 0.1;
				} foreach _allSquads as AiSquadBrain;
				
				sleep 1;
			};
			
			_self.IsRunning = false;
		};
		
		public method RunAsync()
		{
			_self.IsRunning = true;
			[] spawn _self.Run;
		};
		
		private static method Start()
		{
			private ["_aiService" as IService, "_aiUnitBalancer" as IAiUnitBalancer];
			
			sleep 5;
		
			_aiUnitBalancer = [gServer.UnitCounter, gCommon.MissionParametersHandler.AiWestFrequency, gCommon.MissionParametersHandler.AiEastFrequency] new AiUnitBalancer;
			
			_aiService = [
				gServer.AiSquadHandler,
				//_aiUnitBalancer, 
				new Intrusion.Tests.Helpers.TestAiUnitBalancer,
				gServerConfig.Gameplay,
				gCommonConfig.MapMarkers,
				gCommonConfig.Professions,
				gServer.SquadHandler, 
				//gCommon.MissionParametersHandler.AiSkillLevel, 
				gServer.ResourceLocationHandler,
				//gServer.InventedTechnologiesCollection,
				gServer.UnitSpawner,
				new ResourceLocationConfig,
				//gCommonConfig.Professions,
				new AiUnitsConfig, 
				//new AiWeaponsConfig
				gServer.VehicleSpawner,
			] new AiService;
			
			call _aiService.RunAsync;
		};
		
		public static method StartAsync()
		{
			[] spawn _self.Start;
		};
	};
};
