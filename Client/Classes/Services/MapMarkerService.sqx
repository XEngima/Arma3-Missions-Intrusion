/*
 * Name:	MapMarkerService
 * Date:	2018-12-16
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Updates the player's map with map markers (observations not included).
 */

using Intrusion.Common;
using Sqx.Markers;

namespace Intrusion.Client
{
	public class MapMarkerService
	{
		private static fields ["_mMapMarkersConfig" as IMapMarkersConfig, "_mProfessionConfig" as IProfessionConfig, "_mSquadMarkersConfig" as ISquadMarkersConfig];
		private static fields ["_mMapMarkers" as Array /* of Marker */, "_mBaseMarker" as Marker, "_mSquadVehicleMarker" as Marker, "_mSquadMemberMarkers" as Array /* of Marker */];
		
		// Initializes the observation service.
		public static method Init("_mapMarkersConfig" as IMapMarkersConfig, "_professionConfig" as IProfessionConfig, "_squadMarkersConfig" as ISquadMarkersConfig) {
			_mMapMarkersConfig = _mapMarkersConfig;
			_mProfessionConfig = _professionConfig;
			_mSquadMarkersConfig = _squadMarkersConfig;
			_mSquadMemberMarkers = [];
		
			_mMapMarkers = [];
			_mBaseMarker = classNull;
			_mSquadVehicleMarker = [[0,0,0], _squadMarkersConfig.SquadVehicleMarkerType, "ColorBlufor", "Vehicle", false] call Marker.CreateIconMarkerLocal;
		};
		
		// Gets a marker tagged with a certain group.
		// _group (Group): The group whose marker to get.
		// Returns (Marker): The marker associated with the group. classNull if such a marker was not found.
		private static method Marker GetMarkerByGroup("_group" as Group)
		{
			scopeName "main";
			
			{
				private ["_marker" as Marker];
				_marker = _x as Marker;

				if (_marker.Tag == _group) then {
					_marker breakOut "main";
				};
			} foreach _mMapMarkers as Marker;
			
			classNull
		};
		
		// Updates all allied forces' markers.
		private static method UpdateAlliedForcesMarkers()
		{
        	private ["_squadCollection" as SquadCollection, "_squad" as Squad, "_leader" as Object, "_profession" as Profession, "_marker" as Marker, "_professionType" as ProfessionType];
        	private ["_squadIsNearVehicle" as Boolean, "_group" as Group, "_mapMarkersToKeep" as Array];

			_squadCollection = call PublicVariableHandler.GetSquads;
			
			// Remove all groups that no longer exist or is empty
			_mapMarkersToKeep = [];
			{
				private ["_marker" as Marker];
				_marker = _x as Marker;
				
				_group = _marker.Tag as Group;
				
				if (isNull _group || { count units _group == 0 }) then {
					call _marker.Hide;
				}
				else {
					_mapMarkersToKeep pushBack _marker;
				};
			} foreach _mMapMarkers as Marker;
			
			_mMapMarkers = _mapMarkersToKeep;
			
			
			// Update all groups
    		{
    			private ["_group" as Group];
    			_group = _x as Group;
				_leader = leader _group;

				if (!isNull _leader && side _group == playerSide) then {
        			_squad = [_group] call _squadCollection.GetSquadByGroup;
        			
        			_professionType = ProfessionType.Infantry;
        			_squadIsNearVehicle = false;
        			
        			if (!isNull _squad) then {
        				_professionType = _squad.ProfessionType;
	    				
        				// If squad units are more than 50 meters from all of their vehicles, they are not near it.
        				{
        					private ["_vehicle" as IntrusionVehicle];
        					_vehicle = _x as IntrusionVehicle;
        					
        					if (_vehicle.OwnerGroup == _group) then {
	        					if ( { _x distance _vehicle.Vehicle < 50 } count units _squad.Group > 0 ) then {
	        						_squadIsNearVehicle = true;
	        					};
        					};
        				} foreach (call PublicVariableHandler.GetIntrusionVehicles) as Object;
    				};
    				
    				if (!_squadIsNearVehicle) then {
	        			_professionType = ProfessionType.Infantry;
    				};
        			
        			_marker = [_group] call _self.GetMarkerByGroup;
					_profession = [_professionType] call _mProfessionConfig.GetProfession;
        			
        			if (isNull _marker) then {
						_marker = [getPos _leader, _profession.MarkerTypeFriendly, "ColorBlufor"] call Marker.CreateIconMarkerLocal;
						_marker.Tag = _group;
						_mMapMarkers pushBack _marker;
        			}
        			else {
        				[getPos _leader] call _marker.SetPosition;
        				[_profession.MarkerTypeFriendly] call _marker.SetType;
        			};
				};
    		} foreach allGroups as Group;
		};
		
		// Updates the squad's vehicle marker.
		private static method UpdateSquadVehicleMarker()
		{
			private ["_intrusionVehicles" as Array /* of IntrusionVehicle */, "_intrusionVehicle" as IntrusionVehicle];
			
			_intrusionVehicle = classNull;
			_intrusionVehicles = call PublicVariableHandler.GetIntrusionVehicles;
			
			{
				if (_x.OwnerGroup == group player) then {
					_intrusionVehicle = _x;
				};
			} foreach _intrusionVehicles as IntrusionVehicle;
			
			// If the group has a vehicle, update its marker on the map
			if (!isNull _intrusionVehicle) then {
				[getPos _intrusionVehicle.Vehicle] call _mSquadVehicleMarker.SetPosition;	
				call _mSquadVehicleMarker.Show;
			}
			else {
				call _mSquadVehicleMarker.Hide;
			};
		};
		
		// Updates the base marker.
		private static method UpdateAlliedBaseMarker()
		{
			private ["_markerInfo" as MapMarkerInfo];
			[str _this + " MapMarkerService.UpdateAlliedBaseMarker"] call LogHandler.Debug;
			
			_markerInfo = [playerSide] call _mMapMarkersConfig.GetMapMarkerInfo;
			
			if (isNull _mBaseMarker) then {
				_mBaseMarker = [getMarkerPos _markerInfo.BaseMarkerName, _markerInfo.BaseMarkerIconType, "ColorBlufor"] call Marker.CreateIconMarkerLocal;
			};
			
			[(getMarkerPos (_markerInfo.BaseMarkerName))] call _mBaseMarker.SetPosition;
		};
		
		// Updates player and all squad members.
		private static method UpdateSquadMemberMarkers()
		{
			private ["_markedUnits" as Array /* of Object */, "_markersToKeep" as Array /* of Marker */];
			private ["_marker" as Marker];
			
			_markedUnits = [];
			_markersToKeep = [];
		
			// Remove markers that are not squad members (if anyone has left or died or so)
			{
				_marker = _x;
				
				// If the existing marker is for a squad member AND the member is not in a vehicle, then update marker position and keep it
				if (_marker.Tag in units group player && {_marker.Tag == vehicle _marker.Tag}) then {
					[getPos _marker.Tag] call _marker.SetPosition;
					_markedUnits pushBack _marker.Tag;
					_markersToKeep pushBack _marker;
				}
				else {
					call _marker.Hide;
				};
			} foreach _mSquadMemberMarkers as Marker;
			
			// See if there are any new units that ar not yet marked on the map
			{
				if (!(_x in _markedUnits) && {_x == vehicle _x}) then {
					_marker = [getPos _x, _mSquadMarkersConfig.SquadMemberMarkerType, "ColorBlufor"] call Marker.CreateIconMarkerLocal;
					_marker.Tag = _x;
					
					if (_x == player) then {
						["Me"] call _marker.SetText;
					};
					
					_markersToKeep pushBack _marker;
					_markedUnits pushBack _x;
				};
			} foreach (units group player) as Object;
			
			_mSquadMemberMarkers = _markersToKeep;
		};
		
        // Runs the service.
        private static method Run {
        	waitUntil { call PublicVariableHandler.SquadsHasValue };
			waitUntil { call PublicVariableHandler.IntrusionVehiclesHasValue };

        	while { true } do {
        		call _self.UpdateAlliedBaseMarker;
        		call _self.UpdateAlliedForcesMarkers;
        		call _self.UpdateSquadVehicleMarker;
        		call _self.UpdateSquadMemberMarkers;
        		
        		sleep 1;
        	};
		};
		
		// Starts the service
		public static method RunAsync {
			[] spawn _self.Run;
		};
	};
};
