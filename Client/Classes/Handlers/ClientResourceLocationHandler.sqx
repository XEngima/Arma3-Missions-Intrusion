/*
 * Name:	ClientResourceLocationHandler
 * Date:	2017-11-16
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Handles all about resource locations for the current client.
 */

using Intrusion.Common;
using Intrusion.Communication;
 
namespace Intrusion.Client
{
	public class ClientResourceLocationHandler
	{
		// Called locally when a player captures a flag.
		private static method OnActionCaptureFlag("_flag" as Object) {
			private _markerName = _flag getVariable ["MarkerName", ""];
			
			//["ResourceLocationHandler.SetNewFlagOwner", [_markerName, playerSide]] call Remote.Invoke;
			["ServerEventReciever.OnFlagCaptured", [_flag, _markerName, playerSide, player]] call Remote.Invoke;
		};
	
		// Initializes the resource location actions for the first time.
		public static method Init {
			[] spawn _self.DoInitResourceLocations;
		};
		
		// Updates the resource location actions for the player.
		// _resourceLocations (Array): An array containing the resource locations.
		public static method UpdateResourceLocations("_resourceLocationCollection" as ResourceLocationCollection)
		{
			private ["_resourceLocations" as Array /* of ResourceLocation */];
			
			_resourceLocations = call _resourceLocationCollection.GetResourceLocations;
			
			{
				[_x.MarkerName, _x.Side, _x.DominantSide] call _self.UpdateMarkerColor;
			} foreach _resourceLocations as ResourceLocation;
		};
		
		// Initializes the resource location actions for the first time.
		private static method DoInitResourceLocations {
			waitUntil { call PublicVariableHandler.ResourceLocationCollectionHasValue };
			[call PublicVariableHandler.GetResourceLocationCollection] call _self.UpdateResourceLocations;
		};
		
		// Updates the marker color with the color reflecting the current side.
		// _markerName (String): The name of the flag marker.
		// _ownerSide (Side): The side owning the marker.
		private static method UpdateMarkerColor("_markerName" as String, "_ownerSide" as Side, "_dominantSide" as Side)
		{
			if (_ownerSide == civilian) then {
				if (_dominantSide == civilian) then {
					_markerName setMarkerAlphaLocal 1;
				}
				else {
					_markerName setMarkerAlphaLocal 0.3;
				};
			}
			else {
				if (_ownerSide == playerSide) then {
					if (_dominantSide == playerSide || _dominantSide == civilian) then {
						_markerName setMarkerColorLocal "ColorWest";
						_markerName setMarkerAlphaLocal 1;
					}
					else {
						_markerName setMarkerColorLocal "ColorWest";
						_markerName setMarkerAlphaLocal 0.3;
					};
				}
				else {
					if (_dominantSide != playerSide || _dominantSide == civilian) then {
						_markerName setMarkerColorLocal "ColorEast";
						_markerName setMarkerAlphaLocal 1;
					}
					else {
						_markerName setMarkerColorLocal "ColorEast";
						_markerName setMarkerAlphaLocal 0.3;
					};
				};
			};
		};
	};
};
