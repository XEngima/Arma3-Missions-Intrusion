/*
 * Name:	Scientist
 * Date:	2017-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A class modeling a side's R&D manager refining generated technology (i.e. unlocks new technology).
 */

using Sqx.Collections;
using Intrusion.Common;

namespace Intrusion.Server
{
	public class Scientist : IScientist
	{
		private fields ["_mResourceBook" as ResourceBook, "_mTechnologyConfig" as ITechnologyConfig, "_mMessageHandler" as IMessageHandler];
		private fields ["_mInventedTechnologiesCollection" as InventedTechnologiesCollection, "_mSide" as Side];
		private fields ["_mAllTechnologiesInvented" as Boolean];
		
		// Creates a Sceintist object.
		public constructor("_side" as Side, "_resourceBook" as ResourceBook, "_technologyConfig" as ITechnologyConfig, "_messageHandler" as IMessageHandler, "_inventedTechnologiesCollection" as InventedTechnologiesCollection, ["_firstTechToResearch" as Technology, 1 as Technology])
		{
			_mSide = _side;
			_mResourceBook = _resourceBook;
			_mTechnologyConfig = _technologyConfig;
			_mMessageHandler = _messageHandler;
			_mInventedTechnologiesCollection = _inventedTechnologiesCollection;
			
			_self.CurrentTechnology = [_firstTechToResearch] call _mTechnologyConfig.GetTechnology;
			_mAllTechnologiesInvented = false;
		};
		
		// Gets the technology currently being researched.
		public property TechnologyInfo CurrentTechnology { get; private set;};
		
		// Sets the current technology as invented, notifies players, and starts research on next.
		private method SetTechnologyAsInvented()
		{
			private ["_technology" as Technology];
			
			[_mSide, _self.CurrentTechnology.Technology] call _mInventedTechnologiesCollection.AddInventedTechnology;
			["Your side has established '" + _self.CurrentTechnology.Name + "'", _mSide] call _mMessageHandler.ShowHint;
			
			// Get next technology to research
			_technology = (_self.CurrentTechnology.Technology as Scalar) + 1 as Technology;

			if (_technology == Technology.EndOfTechnologies) then {
				_mAllTechnologiesInvented = true;
			}
			else {
				_self.CurrentTechnology = [_technology] call _mTechnologyConfig.GetTechnology;
			};
		};
		
		// Performs research and development, and notifies players when different technology progress steps are achieved.
		public method PerformResearch()
		{
			if (!_mAllTechnologiesInvented) then {
				if (_mResourceBook.TotalResearch >= _self.CurrentTechnology.RequiredResearch) then {
					call _self.SetTechnologyAsInvented;
				};
			};
		};

		// Gets whether a technology is invented yet or not.
		public method Boolean TechnologyIsInvented("_technology" as Technology)
		{
			[_mSide, _technology] call _mInventedTechnologiesCollection.SideTechnologyIsInvented
		};
	};
};
