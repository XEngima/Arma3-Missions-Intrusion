/*
 * Name:	OrderQueueTests
 * Date:	2018-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * Tests for the OrderQueue object.
 */

using Sqx.UnitTest;
using Intrusion.Server;

namespace Tests.Server
{
	public class OrderQueueTests
	{
		// Creates a OrderQueueTests object.
		public static method Init {
			["Tests.Server", "OrderQueueTests", [
				"OrderQueueEmpty_NothingChanged_CurrentIsNull",
				"OrderQueueEmpty_OrderAdded_SetAsCurrent",
				"OrderQueueEmpty_TwoOrdersAdded_FirstIsCurrent",
				"TwoOrders_NextOrderSelected_SecondIsCurrent",
				"TwoOrdersButNoUnitsInGroups_NextOrderSelected_CurrentIsNull",
				"ThreeOrders_RemoveCurrentTwoTimes_ThirdIsCurrent",
				"ThreeOrders_SecondIsEmpty_SecondIsRemoved"
			]] call TestEngine.Register;
		};
		
		private static fields ["_mOrderQueue" as OrderQueue];
		
		private static method BeforeEach {
			_mOrderQueue = new OrderQueue;
		};
		
		private static method OrderQueueEmpty_NothingChanged_CurrentIsNull {
			private ["_orderQueue" as OrderQueue];
		
			// Arrange
			_orderQueue = new OrderQueue;
			
			// Act
			
			// Assert
			[_orderQueue.CurrentOrder, "Current order should be null."] call Assert.IsNull;
			[0, call _orderQueue.Count, "Wrong number of orders in the queue."] call Assert.AreEqual;
		};
		
		private static method OrderQueueEmpty_OrderAdded_SetAsCurrent {
			private ["_group" as Group];
		
			// Arrange
			_group = createGroup west;
			
			// Act
			[_group] call _mOrderQueue.PutOrder;
			
			// Assert
			[_group, _mOrderQueue.CurrentOrder, "Added order does not match current order."] call Assert.AreEqual;
			[1, call _mOrderQueue.Count, "Wrong number of orders in the queue."] call Assert.AreEqual;
						
			// Cleanup
			deleteGroup _group;
		};
		
		private static method OrderQueueEmpty_TwoOrdersAdded_FirstIsCurrent {
			private ["_group1" as Group, "_group2" as Group];
		
			// Arrange
			_group1 = createGroup west;
			_group2 = createGroup east;
			
			// Act
			[_group1] call _mOrderQueue.PutOrder;
			[_group2] call _mOrderQueue.PutOrder;
			
			// Assert
			[_group1, _mOrderQueue.CurrentOrder, "Wrong order is the current."] call Assert.AreEqual;
			[2, call _mOrderQueue.Count, "Wrong number of orders in the queue."] call Assert.AreEqual;
			
			// Cleanup
			deleteGroup _group1;
			deleteGroup _group2;
		};
		
		private static method TwoOrders_NextOrderSelected_SecondIsCurrent {
			private ["_group1" as Group, "_group2" as Group];
		
			// Arrange
			_group1 = createGroup west;
			_group2 = createGroup west;
			"B_Soldier_F" createUnit [[0,0,100], _group1];
			"B_Soldier_F" createUnit [[0,0,100], _group2];
			
			[_group1] call _mOrderQueue.PutOrder;
			[_group2] call _mOrderQueue.PutOrder;
			
			// Act
			call _mOrderQueue.RemoveCurrentOrder;
			
			// Assert
			[_group2, _mOrderQueue.CurrentOrder, "Wrong order is the current."] call Assert.AreEqual;
			[1, call _mOrderQueue.Count, "Wrong number of orders in the queue."] call Assert.AreEqual;
			
			// Cleanup
			deleteVehicle (units _group1 select 1);
			deleteVehicle (units _group2 select 1);
			deleteGroup _group1;
			deleteGroup _group2;
		};
		
		private static method TwoOrdersButNoUnitsInGroups_NextOrderSelected_CurrentIsNull {
			private ["_group1" as Group, "_group2" as Group];
		
			// Arrange
			_group1 = createGroup west;
			_group2 = createGroup west;
			
			[_group1] call _mOrderQueue.PutOrder;
			[_group2] call _mOrderQueue.PutOrder;
			
			// Act
			call _mOrderQueue.RemoveCurrentOrder;
			
			// Assert
			[0, call _mOrderQueue.Count, "Wrong number of orders in the queue."] call Assert.AreEqual;
			[_mOrderQueue.CurrentOrder, "All orders should be gone."] call Assert.IsNull;
			
			// Cleanup
			deleteGroup _group1;
			deleteGroup _group2;
		};
		
		private static method ThreeOrders_RemoveCurrentTwoTimes_ThirdIsCurrent {
			private ["_group1" as Group, "_group2" as Group, "_group3" as Group];
		
			// Arrange
			_group1 = createGroup west;
			_group2 = createGroup west;
			_group3 = createGroup west;
			"B_Soldier_F" createUnit [[0,0,100], _group1];
			"B_Soldier_F" createUnit [[0,0,100], _group2];
			"B_Soldier_F" createUnit [[0,0,100], _group3];
			
			[_group1] call _mOrderQueue.PutOrder;
			[_group2] call _mOrderQueue.PutOrder;
			[_group3] call _mOrderQueue.PutOrder;
			
			// Act
			call _mOrderQueue.RemoveCurrentOrder;
			call _mOrderQueue.RemoveCurrentOrder;
			
			// Assert
			[_group3, _mOrderQueue.CurrentOrder, "Wrong order is the current."] call Assert.AreEqual;
			[1, call _mOrderQueue.Count, "Wrong number of orders in the queue."] call Assert.AreEqual;
			
			// Cleanup
			deleteVehicle (units _group1 select 1);
			deleteVehicle (units _group2 select 1);
			deleteVehicle (units _group3 select 1);
			deleteGroup _group1;
			deleteGroup _group2;
			deleteGroup _group3;
		};
		
		private static method ThreeOrders_SecondIsEmpty_SecondIsRemoved {
			private ["_group1" as Group, "_group2" as Group, "_group3" as Group];
		
			// Arrange
			_group1 = createGroup west;
			_group2 = createGroup west;
			_group3 = createGroup west;
			"B_Soldier_F" createUnit [[0,0,100], _group1];
			"B_Soldier_F" createUnit [[0,0,100], _group3];
			
			[_group1] call _mOrderQueue.PutOrder;
			[_group2] call _mOrderQueue.PutOrder;
			[_group3] call _mOrderQueue.PutOrder;
			
			// Act
			call _mOrderQueue.RemoveCurrentOrder;
			
			// Assert
			[_group3, _mOrderQueue.CurrentOrder, "Wrong order is the current."] call Assert.AreEqual;
			[1, call _mOrderQueue.Count, "Wrong number of orders in the queue."] call Assert.AreEqual;
			
			// Cleanup
			deleteVehicle (units _group1 select 1);
			deleteVehicle (units _group3 select 1);
			deleteGroup _group1;
			deleteGroup _group2;
			deleteGroup _group3;
		};
	};
};
