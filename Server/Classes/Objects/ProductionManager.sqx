/*
 * Name:	ProductionManager
 * Date:	2017-11-19
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A class modeling a side's production manager, refining generated production (i.e. spawns new
 * vehicles and weapons).
 */
using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Server
{
	public class ProductionManager : IGroupChangingProfessionListener
	{
		private fields ["_mVehicleSpawner" as IVehicleSpawner, "_mResourceBook" as ResourceBook, "_mOrderQueue" as OrderQueue];
	
		// Creates a ProductionManager object.
		public constructor {
			params ["_side" as Side, "_vehicleSpawner" as IVehicleSpawner, "_resourceBook" as ResourceBook, ["_orderQueue" as OrderQueue, classNull]];
			_self.Side = _side;
			_mVehicleSpawner = _vehicleSpawner;
			_mResourceBook = _resourceBook;
			
			_mOrderQueue = _orderQueue;
			
			// If orderQueue is null, then we are in production mode (not in unit testing)
			if (isNull _mOrderQueue) then {
				_mOrderQueue = new OrderQueue;
			
				// Register the class to the global group handler to recieve notifications 
				// when groups on the side is changing its profession.
				[_self] call SquadHandler.RegisterGroupChangingProfessionListener;
			};
		};
		
		// Gets the production manager's side.
		public property Side Side { get; private set; };
		
		// Called when a group on the production manager's side changes its profession.
		// _group (Group): The group that is changing its profession.
		// _professionType (ProfessionType): The group's new profession type.
		public method OnGroupChangingProfession {
			params ["_group" as Group, "_professionType" as ProfessionType];
			private ["_order" as Order, "_vehicleClassNames" as Array, "_vehicleClassName" as String, "_professionVehicle" as ProVehicleInfo];
			
			[_group] call _mOrderQueue.RemoveOrdersByGroup;
			
			_vehicleClassNames = [side _group, _professionType] call VehicleClassNamesConfig.GetProVehiclesClassNames;
			_vehicleClassName = selectRandom _vehicleClassNames;
			_professionVehicle = [_professionType] call ProVehiclesConfig.GetVehicle;
			
			_order = [_group, _vehicleClassName, _professionVehicle.Cost] new Order;
			[_order] call _mOrderQueue.PutOrder;
		};
		
		// Produces vehicles from the production queue.
		public method Produce {
			private ["_tryOneMore" as Boolean];
			
			_tryOneMore = true;
		
			while { _tryOneMore && {!isNull _mOrderQueue.CurrentOrder} } do {
				private _totalProduction = _mResourceBook.TotalProduction;
				private _cost = _mOrderQueue.CurrentOrder.ProductionCost;
				
				if (_totalProduction >= _cost) then {
					[_mOrderQueue.CurrentOrder.VehicleClassName] call _mVehicleSpawner.SpawnVehicleOnCarpark;
					
					// Tell the group
					["ClientMessageHandler.ShowMessage", ["Your vehicle is ready and waiting at the base.", _mOrderQueue.CurrentOrder.Group]] call Remote.Invoke;
					
					call _mOrderQueue.RemoveCurrentOrder;
					[_cost] call _mResourceBook.WithdrawProduction;
				}
				else {
					_tryOneMore = false;
				};
			};
		};
	};
};
