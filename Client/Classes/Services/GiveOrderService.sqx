/*
 * Name:	GiveOrderService
 * Date:	2019-02-14
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A service that is used to when a brigade leader is giving order.
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Client
{
	public class GiveOrderService : IService
	{
		private fields ["_mWaitingForMapClick" as Boolean, "_mMapPos" as Array, "_mSelectedGroup" as Group, "_mOrderType" as OrderType, "_mMissionTime" as IMissionTime];
	
		// Creates a GiveOrderService object.
		public constructor("_missionTime" as IMissionTime)
		{
			_mMissionTime = _missionTime;
			call _self.Reset;
		};
		
		private method Reset()
		{
			_self.IsRunning = false;
			_mWaitingForMapClick = false;
			_mMapPos = [];
			_mSelectedGroup = grpNull;
			_mOrderType = OrderType.None;
		};
		
		public property Boolean IsRunning { get; private set; };
		
		public method SendMapClick("_pos" as Array)
		{
			if (_mWaitingForMapClick) then {
				_mMapPos = _pos;
				_mWaitingForMapClick = false;
			};
		};
		
		public method SendOrderType("_orderType" as OrderType)
		{
			_mOrderType = _orderType;
		};
		
		private method Group FindClosestGroup("_pos" as Array)
		{
		    private ["_group" as Group];
		    private ["_closestDistance" as Scalar];
		    
		    _group = group player; // Default
		    _closestDistance = vehicle player distance _pos;
		    
		    {
		        if (vehicle leader _x distance _pos < _closestDistance) then {
		            _closestDistance = vehicle leader _x distance _pos;
		            _group = _x;
		        };
		    } foreach allGroups as Group;
		    
		    _group
		};
		
		private method ResourceLocation FindClosestResourceLocation("_pos" as Array)
		{
			private ["_locationCollection" as ResourceLocationCollection];
			
			_locationCollection = call PublicVariableHandler.GetResourceLocationCollection;
			
			([_pos] call _locationCollection.GetClosestResourceLocation)
		};
		
		private method Run()
		{
			private ["_orderResult" as String, "_enoughWaitingTime" as Scalar, "_location" as ResourceLocation, "_orderTimeoutTime" as Scalar];
				
			_orderResult = "Order cancelled.";
		
			// Open the map and ask player to select a group
			openMap true;
			player sideChat "Select a group by clicking near it on the map.";
			
			_mWaitingForMapClick = true;
			_enoughWaitingTime = time + 30;
			waitUntil {	!_mWaitingForMapClick || time > _enoughWaitingTime };
			
			if (!_mWaitingForMapClick) then
			{
				_mSelectedGroup = [_mMapPos] call _self.FindClosestGroup;
				player sideChat ("Group '" + name leader _mSelectedGroup + "' selected.");
				
				// Open the give order GUI
				
				call OrderDialog.ShowDialog;
				
				if (_mOrderType != OrderType.None) then
				{
					_mWaitingForMapClick = true;
					_enoughWaitingTime = time + 30;
					
					_orderTimeoutTime = 0;
					if (_mOrderType == OrderType.Defend) then {
						_orderTimeoutTime = (call _mMissionTime.GetMissionTime) + 20 * 60;
					};
					
					waitUntil {	!_mWaitingForMapClick || time > _enoughWaitingTime};
					
					if (!_mWaitingForMapClick) then {
						// Is the target area a resource location?
						_location = [_mMapPos] call _self.FindClosestResourceLocation;
						
						if (_mMapPos distance getMarkerPos _location.MarkerName > 150) then {
							_location = classNull;
						};
					
						// Update order info
						private _orderInfo = ([_mOrderType] call OrderTypeMeta.ToString);
						
						if (!isNull _location) then {
							_orderInfo = _orderInfo + " " + markerText _location.MarkerName;
						};
						
						_orderInfo = _orderInfo + "*";
						
						_mSelectedGroup setVariable ["IntrusionOrderInfo", _orderInfo, true];
						
						if (!(isPlayer leader _mSelectedGroup)) then {
							["ServerEventReciever.OnBrigadeLeaderGivingOrder", [[_mSelectedGroup, _mOrderType, _mMapPos, _location, _orderTimeoutTime] new Order]] call Remote.Invoke;
							_orderResult = ("AI group '" + name leader _mSelectedGroup + "' have a new order.");
						}
						else {
							["ClientEventReciever.OnBrigadeLeaderGivingOrder", [[_mSelectedGroup, _mOrderType, _mMapPos, _location, _orderTimeoutTime] new Order]] call Remote.Invoke;
							_orderResult = ("Human group '" + name leader _mSelectedGroup + "' have a new order.");
						};
					};
				};
			};
			
			player sideChat _orderResult;
		};
		
		public method RunAsync()
		{
			if (!_self.IsRunning) then {
				_self.IsRunning = true;
				call _self.Reset;
				[] spawn _self.Run;
			};
		};
	};
};
