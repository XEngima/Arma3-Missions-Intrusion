/*
 * Name:	ProfessionDialog
 * Date:	2017-11-29
 * Version: 1.0
 * Author:  Engima
 *
 * Description:
 * A dialog in which the user can change profession.
 */

using Intrusion.Common;
using Intrusion.Communication;

namespace Intrusion.Client
{
	public class ProfessionDialog
	{
		private static fields ["_mOptions" as Array /* of ProfessionType */, "_mProfessionConfig" as IProfessionConfig];
	
		// Initializes the profession dialog.
		public static method Init("_professionConfig" as IProfessionConfig)
		{
			_mProfessionConfig = _professionConfig;
			_mOptions = [];
			_self.IsOpen = false;
			_self.SelectedProfession = ProfessionType.Undefined;
		};
		
		// Gets or sets the profession type that the user has selected.
		public static property ProfessionType SelectedProfession { get; set; };
		
		// Gets whether the dialog is open or not.
		public static property Boolean IsOpen { get; private set; };
		
		// Reads the selected profession type from the combo box
		// Returns (Profession): The selected profession. classNull if no profession is selected.
		private static method ProfessionType GetSelectedProfessionTypeFromCombo()
		{
			private _selectedIndex = lbCurSel (DialogControl.GroupProfessionListBox as Scalar);
			
			_mOptions select _selectedIndex
		};

		// Adds all professions to the combo box.
		private static method AddProfessions()
		{
			private ["_options" as Array, "_sGroupSize" as String, "_sMaxGroupsCount" as String];
			
            _options = [];
            
			{
				_sGroupSize = str _x.MinGroupSize + "-" + str _x.MaxGroupSize;
				if (_x.MinGroupSize == _x.MaxGroupSize) then {
					_sGroupSize = str _x.MinGroupSize;
				};
				
				_sMaxGroupsCount = "";
				if (_x.MaxGroupsCount >= 0) then {
					_sMaxGroupsCount = ", max " + str _x.MaxGroupsCount + " groups";
				};
				
				lbAdd [DialogControl.GroupProfessionCombo, (_x.Name + " (" + _sGroupSize + " members" + _sMaxGroupsCount + ")")];
				lbAdd [DialogControl.GroupProfessionListBox, _x.Name];
				_options pushBack _x.Type;
			} foreach (call _mProfessionConfig.GetAllProfessions) as Profession;
            
            _mOptions = _options;
		};
	
		// Shows the dialog.
		// Returns (DialogResult): A dialog result.
		public static method DialogResult ShowDialog()
		{
			waitUntil { !dialog };
			
			// Create the dialog
            createDialog "IntProfessionDialog";
			
			call _self.AddProfessions;
			// TODO: This should take groups current type and find that index
			// and select that instead off 0.
			lbSetCurSel [DialogControl.GroupProfessionListBox, 0]; 
			
			_self.IsOpen = true;

			// Wait until dialog is closed
			waitUntil { !dialog };
			
			// Return OK.
			DialogResult.Ok
		};
		
		// Closes the dialog if it is open.
		public static method CloseDialog()
		{
			if (_self.IsOpen) then {
				closeDialog 0;
			};
		};
		
 		// Change Image and text when a new selection is made in the listbox.
		// Called by the .hpp-dialog
		public static method OnListSelectChanged {
			private [
				"_profession" as Profession, 
				"_vehicles" as ProfessionVehicleClassNamesInfo, 
				"_technology" as TechnologyInfo,
				"_vehiclesConfig" as IProfessionVehiclesConfig,
				"_vehicleInfo" as ProfessionVehicleInfo,
				"_className" as String
			];
			private _model = "";
			private _vehicleNames = []; 
			private _ctrlVehicleModel = (findDisplay 2999) displayCtrl 2230;
			private _ctrlDescription = (findDisplay 2999) displayCtrl 2215;
			//_profession = call _self.GetSelectedProfessionTypeFromCombo;
			_profession = [call _self.GetSelectedProfessionTypeFromCombo] call _mProfessionConfig.GetProfession;
			_vehicles = [side player, _profession.Type] call gCommonConfig.VehicleClassNames.GetProVehiclesClassNameInfo;

 			if (!isNull _vehicles) then {

 				if (count _vehicles.ClassNames > 0) then {
					_className = _vehicles.ClassNames select ((count _vehicles.ClassNames) - 1);
					_model = (getText(configFile >> "cfgVehicles" >> _className >> "model"));
					_ctrlVehicleModel ctrlSetModel _model;
					_ctrlVehicleModel ctrlShow true;
				};

 				{
					_vehicleNames pushBackUnique (getText(configFile >> "cfgVehicles" >> _x >> "displayName"));
				} foreach (_vehicles.ClassNames);
			} else {
				/* Set a empty as vehicle picture */
				_ctrlVehicleModel ctrlSetModel "\a3\Ui_f\objects\Compass.p3d";
				_ctrlVehicleModel ctrlShow false;
			};

 			_technology = [_profession.RequiredTechnology] call gCommonConfig.Technology.GetTechnology;
			private _requiredResearch = "";
			private _cost = "";
			if (!isNull _technology) then {
				_requiredResearch = str _technology.RequiredResearch
			};

 			switch (DebugConfig.Mode) do {
				case MissionConfigMode.Production:
				{
					_vehiclesConfig = new ProfessionVehiclesProdConfig;
				};
				case MissionConfigMode.Test:
				{
					_vehiclesConfig = new ProfessionVehiclesTestConfig;
				};
				case MissionConfigMode.Develop:
				{
					_vehiclesConfig = new ProfessionVehiclesDevConfig;
				};
			};

 			_vehicleInfo = [_profession.Type] call _vehiclesConfig.GetVehicle;
			if (!isNull _vehicleInfo) then {
				_cost = str _vehicleInfo.Cost;
			};


 			private _description = [
				"<t size='2.2' underline='true'>" + _profession.Name + "</t>",
				"Required Research: " + _requiredResearch,
				"Vehicle: " + (_vehicleNames joinString ", "),
				"Vehicle cost: " + _cost,
				"Required units in group (max): " + str _profession.MinGroupSize + " (" + str _profession.MaxGroupSize + ")",
				"Max groups with this profession: " + str _profession.MaxGroupsCount
			];
			_ctrlDescription ctrlSetStructuredText parseText (_description joinString "<br/>");
		};
		
		// Sends the group's selected profession to the server.
		// Called by the .hpp-dialog when the OK button is pressed.
		public static method OnOkButtonPressed()
		{
			ProfessionDialog.SelectedProfession = call _self.GetSelectedProfessionTypeFromCombo;
			["SquadHandlerReciever.SetSquadProfession", [group player, ProfessionDialog.SelectedProfession]] call Remote.Invoke;
		};		
		
		// Called by the .hpp-dialog when the Cancel button is pressed.
		public static method OnCancelButtonPressed()
		{
			call _self.CloseDialog;
		};		
	};
};
